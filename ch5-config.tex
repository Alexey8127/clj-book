\chapter{Конфигурация}

\begin{teaser}
В этой главе мы рассмотрим, как сделать проект на Clojure удобным в
настройке. Разберем основы конфигурации: форматы файлов, переменные среды,
библиотеки, их достоинства и недостатки.
\end{teaser}

\section{Постановка проблемы}

В обучающих материалах по Clojure встречаются примеры:

\begin{verbatim}
(def server
  (jetty/run-jetty app {:port 8080}))

(def db
  {:dbtype   "postgres"
   :dbname   "test"
   :user     "ivan"
   :password "test"})
\end{verbatim}

Это веб-сервер на 8080 порту и параметры подключения к базе. Примеры полезны
тем, что их можно скопировать в REPL и оценить результат: открыть страницу в
браузере или прочитать таблицу.

На практике выражения улучшают так, чтобы в них не было конкретных цифр и
строк. С точки зрения проекта плохо, что у веб-сервера явно указан порт. Это
подойдет для документации и примеров, но не промылшенного старта.

Порт 8080, ровно как и другие комбинации нулей и восьмерок, популярен у
разработчиков. Велика вероятность, что порт занят другим веб-сервером. Это
возможно, когда запускают не отдельный сервис, а их связку на время разработки
или тестов.

Код, написанный программистом, проходит несколько стадий. Их набор отличается в
зависимости от фирмы, но в целом это разработка, автоматическое тестирование,
предварительный запуск и боевой режим.

На каждой стадии приложение запускают бок о бок с другими
проектами. Предположение о том, что порт 8080 свободен в любой момент звучит как
утопия. На жаргоне разработчиков это называется <<хардкод>> (анг. hardcode), что
по-другому называют <<прибито гвоздями>>.

Когда приложение работает на <<прибитых>> значениях, это вносит проблемы в его
жизненный цикл. Например, вы не сможете одновременно разрабатывать и тестировать
его. Или два тестовых сервера подключаются к одной базе данных, и вы не
понимаете, почему данные меняются спонтанно.

Приложение не должно решать, на каком порту запускать веб-сервер. Информация об
этом приходит извне. В простом случае это текстовый файл. Приложение читает порт
и запускает сервер именно так, как это нужно на конкретной машине.

В более сложных сценариях этот файл составляет не человек, а специальная
программа~--- менеджер конфигураций. Менеджер хранит информацию о топологии
сети, адреса машин, типы баз данных и параметры подключения к ним. По запросу он
выдает файл для определенной машины или сегмента сети.

Процесс, когда приложению сообщают параметры, а оно принимает их, называется
конфигурацией. Это интересный и важный процесс. Когда он построен удачно, проект
не испытывает трудностей на всех стадиях производства.

\section{Семантика}

Цель конфигурации в том, чтобы управлять программой, не меняя ее код. К этому
приходят с ростом кодовой базы и инфраструктуры. Если у вас мелкий скрипт на
Python, нет ничего зазорного в том, чтобы открыть его в блокноте и поменять
константу. На предприятиях такие скрипты работают годами.

Чем сложнее инфраструктура фирмы, тем больше в ней ограничений. Современные
практики сводят на нет спонтанные изменения в проекте. Например, нельзя сделать
\spverb|git push| напрямую в мастер; запрещен \spverb|merge|, пока \spverb|pull request|
не одобрят два члена команды; приложение не попадет на сервер, пока не
пройдpут тесты.

Это приводит к тому, что малейшее изменение в коде займет около часа, чтобы
попасть на боевой сервер. Правка в конфигурации гораздо дешевле, чем выпуск
новой версии продукта. Из этого следует правило: \emph{если можно вынести что-то
  в конфигурацию, сделайте это сейчас}.

В крупных фирмах практикуют то, что называют \spverb|feature flag|. Это
логическое поле, которое включает целый пласт логики в приложении. Например,
новый интерфейс, систему обработки заявок, улучшенный чат. Значимые обновления
долго тестируют внутри фирмы, но всегда остается риск, что в боевом режиме
\emph{что-то пойдет не так}. Тогда флаг конфигурации меняют на ложь и
перезапускают сервер. Это не только удобно в техническом плане, но экономит
время коллег и сохранит репутацию фирмы.

\section{Цикл конфигурации}

Во время запуска приложение следует регламенту. Это поиск ресурсов, файлов,
обмен данными по сети. В момент подготовки приложение решает, сможет ли оно
продолжить работу.

Конфигурация занимает первую позицию в регламенте. Это логично, поскольку чем
лучше устроено приложение, тем больше его частей опирается на
конфигурацию. Обработка конфигурации это не монолитная задача, а набор
шагов. Перечислим наиболее важные из них.

На первом этапе программа \textbf{читает конфигурацию}. Чаще всего это файл или
переменные среды. Чтобы хранить данные в тексте, уже придуманы специальные
форматы: JSON, EDN, YAML. Приложение несет на борту модуль, чтобы получить из
файла структуру данных. Мы рассмотрим плюсы и минусы основных форматов ниже.

Переменные среды это часть операционной системы. Представьте их как глобальный
словарь в памяти операционной системы. Каждое приложение наследует этот словарь
при запуске. Языки и фреймворки предлагают функции, чтобы считать переменные в
строки и словари.

Файлы и переменные среды дополняют друг друга. Например, приложение читает
данные из файла, но путь к нему ищет в переменных среды. Или в файле опущены
критические данные: пароли, API-ключи. Так поступают, чтобы посторонние
программы не подсмотрели их. Приложение читает параметры из файла, а секретные
ключи из переменных среды.

Продвинутые конфигурации поддерживают теги. Например, в файле перед значением
переменной ставят тег: \spverb|:password #env DB_PASSWORD|. Это значит, что в
поле \spverb|password| следует поместить не строку \spverb|DB_PASSWORD|, а
значение одноименной переменной. Мы рассмотрим теги в следующем разделе.

Первый этап завершается тем, что мы получили структуру данных. Неважно, был ли
это файл, переменные среды или что-то другое. Приложение переходит ко второму
этапу~--- \textbf{выводу типов}.

JSON или YAML выделяют базовые типы: строки, числа, булево и null. Легко
заметить, что среди них нет даты. С помощью дат задают промо-акции или события,
связанные с праздниками. В файлах даты указывают либо строкой в формате ISO,
либо числом секунд c 1 января 1970 года (эпоха UNIX). Специальный код пробегает
про структуре данных и приводит поля к типу, принятому в языке.

Вывод типов применяют и для коллекций. Иногда словари и массивы недостаточны для
комфортной работы. Например, типы событий хранят в виде множества, поскольку
поиск в нем быстрее, чем в массиве. Скалярные значения тоже оборачивают в классы
высокого уровня. Это \spverb|UUID| для идентификаторов, \spverb|PGObject| для
перечисляемых типов и другие.

Переменные среды не настолько гибки, как современные форматы. Если JSON и
аналоги выделяют скаляры, структуры и их комбинации, то переменные несут только
строки. Вывод типов для них не просто желателен, а необходим. Не получится
передать порт в виде строки туда, где ожидают число.

После вывода типов приступают к \textbf{валидации данных}. В главе про spec мы
выяснили, что верный тип не обещает верное значение. Проверка нужна, чтобы в
конфигурации нельзя было указать порт 0, -1 или 80.

Из той же главы мы помним, что иногда значения верны по отдельности, но их
комбинация ошибочна. Пусть в конфигурации задан период акции. Это массив из двух
дат, начало и завершение. Легко перепутать даты местами, и проверка любой даты
на интервал вернет ложь.

После валидации переходят к последней стадии. Приложение определяет, где
\textbf{хранить конфигурацию}. Это может быть глобальная переменная или
компонент системы. Другие части программы читают оттуда параметры.

\section{Ошибки конфигурации}

На каждом этапе может возникнуть ошибка. Не найден файл, ошибка синтаксиса,
неверное поле. В этом случае от системы ожидают двух действий: сообщения об
ошибке и завершения.

Сообщение должно четко отвечать на вопрос, что случилось. Часто программисты
держат в голове только положительный путь и забывают об ошибках. При запуске их
программ виден стек-трейс, который трудно понять. Это грубейшая ошибка.

%% TODO ref

В главе про исключениях мы рассмотрели прием. Если логика состоит из отдельных
шагов, каждый из них оборачивают в \spverb|try/catch|. Исключение, которое
поймали, кидают повторно с понятным сообщением и контекстом.

Когда ошибка случилась на этапе проверки, объясните, какое поле было тому
виной. Мы рассмотрели, как улучшить стандартный отчет спеки. Это требует усилий,
но окупается со временем.

В IT-индустрии одни сотрудники пишут код, а другие управляют им. Ваш
коллега-DevOps не знает, что такое Clojure и не поймет сырой отчет спеки. Рано
или поздно он придет с просьбой доработать сообщения конфигурации. Сделайте это
заранее из уважения к коллегам.

Если с конфигурацией что-то не так, программа не должна работать дальше в
надежде, что все обойдется. Возможна ситуация, когда один из параметров не
задан, но программа к нему не обращается. Избегайте этого: ошибка всплывет в
самый неподходящий момент.

Когда один из шагов конфигурации не сработал, программа выводит сообщение и
завершается с кодом, отличным от нуля. Сообщение пишут в канал \spverb|stderr|,
чтобы подчеркнуть внештатную ситуацию. Продвинутые терминалы подсвечивают текст
из \spverb|stderr| красным цветом.

\section{Практика: загрузчик конфигурации}

Чтобы закрепить теорию, напишем свою систему конфигурации. Это отдельный модуль
примерно на сто строк. Прежде чем садиться за редактор, обдумаем основные
моменты будущего кода.

Будем хранить конфигурацию в JSON-файле. Предположим, фирма недавно перешла на
Clojure, и у DevOps уже написаны скрипты на Python для управления
настройками. Формат EDN усложнит коллегам работу.

Путь к файлу задают в переменной среды \spverb|CONFIG_PATH|. От файла мы ожидаем
порт сервера, параметры базы данных и диапазон дат для промо-акции. Текстовые
даты должны стать объектами \spverb|java.util.Date|. Дата начала строго меньше
окончания.

Готовый словарь запишем в глобальную переменную \spverb|CONFIG|. Если на одном
из шагов случилась ошибка, выводим сообщение и завершаем программу.

Начнем со вспомогательной функции \spverb|exit|. Она принимает код завешения,
текст и параметры форматирования. Если код равен нулю, пишем сообщение в
\spverb|stdout|, иначе в \spverb|stderr|.

\begin{verbatim}
(defn exit
  [code template & args]
  (let [out (if (zero? code) *out* *err*)]
    (binding [*out* out]
      (println (apply format template args))))
  (System/exit code))
\end{verbatim}

Переходим к загрузчику. Это набор шагов, каждый из которых принимает результат
предыдущего. Логику каждого описывает имя функции. Совместим вывод типов и
валидацию в одном шаге \spverb|coerce-config|, поскольку технически это вызов
\spverb|s/conform|.

\begin{verbatim}
(defn load-config! []
  (-> (get-config-path)
      (read-config-file)
      (coerce-config)
      (set-config!)))
\end{verbatim}

Напишем логику каждого шага. Функция \spverb|get-config-path| читает переменную
среды и проверяет, есть ли такой файл на диске. Если все в порядке, функция
вернет путь к файлу, иначе вызовет \spverb|exit|:

\begin{verbatim}
(import 'java.io.File)

(defn get-config-path []
  (if-let [filepath (System/getenv "CONFIG_PATH")]
    (if (-> filepath (new File) .exists)
      filepath
      (exit 1 "File %s does not exist" filepath))
    (exit 1 "File path is not set")))
\end{verbatim}

Шаг \spverb|read-config-file| читает файл по его пути. Для разбора JSON служит
библиотека \spverb|cheshire|. Функция \spverb|parse-string| вернет данные из
строки документа.

\begin{verbatim}
(require '[cheshire.core :as json])

(defn read-config-file
  [filepath]
  (try
    (-> filepath slurp (json/parse-string true))
    (catch Exception e
      (exit 1 "Malformed config file: %s" (ex-message e)))))
\end{verbatim}

Стадия проверки и вывода типов займет больше места. Это самый важный этап:
нельзя допустить, чтобы приложение стартовало с неверными параметрами.

Логика \spverb|coerce-config| проста. Мы получаем чистые данные из исходных
функцией \spverb|s/conform|. Ее вызов потенциально несет исключения, поэтому
оборачиваем в \spverb|try/catch|. Если получили ключ \spverb|:invalid|,
формируем отчет об ошибке и выводим в терминал. Для отчета возьмем библиотеку
\spverb|expound|.

\begin{verbatim}
(require '[clojure.spec.alpha :as s])
(require '[expound.alpha :as expound])

(defn coerce-config [config]
  (try
    (let [result (s/conform ::config config)]
      (if (= result ::s/invalid)
        (let [report (expound/expound-str ::config config)]
          (exit 1 "Invalid config values: %s %s" \newline report))
        result))
    (catch Exception e
      (exit 1 "Wrong config values: %s" (ex-message e)))))
\end{verbatim}

Не хватает определения спеки. Откроем файл и изучим его структуру:

\begin{verbatim}
{
    "server_port": 8080,
    "db": {
        "dbtype":   "mysql",
        "dbname":   "book",
        "user":     "ivan",
        "password": "****"
    },
    "event": [
        "2019-07-05T12:00:00",
        "2019-07-12T23:59:59"
    ]
}
\end{verbatim}

\noindent
Опишем спеку сверху вниз. На верхнем уровне это словарь с ключами:

\begin{verbatim}
(s/def ::config
  (s/keys :req-un [::server_port ::db ::event]))
\end{verbatim}

Порт сервера это комбинация двух предикатов: число и вхождение в диапазон.
Проверка на число нужна, чтобы во второй предикат не попали \spverb|nil| и
строка. Это вызовет исключение там, где его не ждали.

\begin{verbatim}
(s/def ::server_port
  (s/and int? #(< 1024 % 65535)))
\end{verbatim}

Для подключения к базе данных укажем готовую спеку из пакета \spverb|jdbc|:

\begin{verbatim}
(require '[clojure.java.jdbc.spec :as jdbc])
(s/def ::db ::jdbc/db-spec)
\end{verbatim}

Событе тоже сложная спека, поэтому опишем ее сверху вниз. Это комбо из кортежа
дат и проверки диапазона:

\begin{verbatim}
(s/def ::event
  (s/and (s/tuple ->date ->date)
         ::date-range))
\end{verbatim}

Спека \spverb|->date| выводит дату из строки. Чтобы не писать ручной разбор,
возьмем функцию \spverb|read-instant-date| из пакета
\spverb|clojure.instant|. Функция лояльна к формату строки и читает неполные
даты, например только год. Обернем ее в \spverb|s/conformer|.

\begin{verbatim}
(require '[clojure.instant :as inst])
(def ->date (s/conformer inst/read-instant-date))
\end{verbatim}

Опишем проверку диапазона. Она принимает пару объектов \spverb|java.util.Date| и
сравнивает их. Даты нельзя сравнивать знаками <<больше>> или <<меньше>>. Причина
кроется в особенности класса \spverb|Date|. Функция \spverb|compare| принимает
два объекта и возвращает -1, 0 и 1 для случаев меньше, равно и больше.

\begin{verbatim}
(s/def ::date-range
  (fn [[date1 date2]]
    (neg? (compare date1 date2))))
\end{verbatim}

Спека готова, и шаг \spverb|coerce-config| тоже завершен. Для тех, кто
торопится: удалите последнюю строку в \spverb|load-config!| и проверьте
результат.

Шаг \spverb|set-config!| пишет словарь в глобальную переменную. Объявим будущую
конфигурацию под именем \spverb|CONFIG|. Верхний регистр подчеркивает, что это
глобальная переменная. В функции вы не затените ее переменной \spverb|config|.

\begin{verbatim}
(def CONFIG nil)

(defn set-config!
  [config]
  (alter-var-root (var CONFIG) (constantly config)))
\end{verbatim}

На старте программы напишите \spverb|(load-config!)|, чтобы в \spverb|CONFIG|
появилась конфигурация. Другие модули импортируют \spverb|CONFIG| в свои модули
и читают нужные им ключи. Вот как запустить сервер или запросить базу с учетом
конфигурации:

\begin{verbatim}
(require '[project.config :refer [CONFIG]])

(def server
  (jetty/run-jetty app {:server_port CONFIG
                        :join? false}))

(jdbc/query (:db CONFIG) "select * from users")
\end{verbatim}

\subsection{Работа над ошибками}

Мы написали загрузчик конфигурации. Он простой и удобный в поддержке: чтобы
добавить поле, расширьте спеку. Это не промышленное решение, но такой загрузчик
подойдет для небольших проектов.

\emph{Преимущество} в том, что в любой момент можно считать конфигурацию
заново. Это особенно удобно в REPL: измените JSON-файл и выполните
\spverb|load-config!| повторно.

\emph{Недостаток} загрузчика в том, что код привязан к функции \spverb|exit|. Ее
вызов завершает программу. Это правильный подход в боевом запуске: нет смысла
продолжать, если параметры неверны. При разработке от этого больше проблем:
любая ошибка завершает REPL. Придется запускать проект заново.

Завершение JVM это слишком радикальное действие. Загрузка конфигурации и реакция
на ошибки должны быть разделены. Наивный способ~--- вызвать
\spverb|load-config!| внутри макроса, который переопределяет
\spverb|exit|. Новая функция не вызывает \spverb|System/exit|, а только кидает
исключение. Напишем отдельную функцию:

\begin{verbatim}
(defn load-config-repl! []
  (with-redefs
    [exit (fn [_ template & args]
            (let [^String message
                  (apply format template args)]
              (throw (new Exception message))))]
    (load-config!)))
\end{verbatim}

Теперь вызов \spverb|load-config-repl!| просто бросит исключение, но не завершит
сеанс.

Более удачное решение в том, чтобы передать в \spverb|load-config!|
дополнительные параметры. Назовем один из них \spverb|:die-fn|. Это функция,
которая принимает исключение. В боевом запуске <<die function>> завершает JVM, в
режиме разработки пишет сообщение в REPL.

Доработайте загрузчик так, чтобы он поддерживал параметр
\spverb|:die-fn|. Продумайте поведение по умолчанию, если он не задан.

\section{Подробнее о переменных среды}

Загрузчик читает данные из файла. Из переменных среды он берет только малую
часть~--- путь к файлу. Изменим загрузчик так, чтобы он читал конфигурацию из
переменных среды и не нуждался в файлах. Чтобы это сделать и понять преимущества
подхода, поговорим о переменных в отрыве от языка.

Переменные среды еще называют ENV, <<энвы>> (анг. <<environment>>, окружающая
среда). Это фундаментальное свойство операционной системы. Представьте
переменные как глобальный словарь, который наполняется во время загрузки. В
словаре записаны основные параметры системы. Это локаль, список папок, где
система ищет бинарные файлы, домашняя директория пользователя и многое другое.

Чтобы увидеть текущие переменные, запустите в терминале \spverb|env| или
\spverb|printenv|. На экране появится пары \spverb|ИМЯ=значение|. Имена
переменных пишут в верхнем регистре, чтобы выделить на общем фоне и подчеркнуть
их приоритет. Большинство систем различают регистр, поэтому \spverb|foo| и
\spverb|FOO| это разные переменные. Пробелы и дефисы недопустимы; лексемы
разделяют подчеркиванием. Фрагмент вывода \spverb|printenv|:

\begin{verbatim}
LANG=en_US.UTF-8
PWD=/Users/ivan
SHELL=/bin/zsh
TERM_PROGRAM=iTerm.app
COMMAND_MODE=unix2003
\end{verbatim}

Каждый процесс получает копию этого словаря. Процесс может добавить или удалить
переменную, но изменения видны только этому процессу и его потомкам. Когда
процесс запускает потомка, он наследует переменные родителя.

Переменные системы не меняются, можно только запустить процесс с их новой
копией. Чтобы изменить набор, который система получает на старте, меняют ее
системные файлы.

\subsection{Локальные и глобальные переменные}

Различают переменные \emph{среды} и \emph{шелла}, они же глобальные и локальные
переменные. Их часто путают начинающие программисты. Откройте терминал и
выполните команду:

\begin{verbatim}
FOO=42
\end{verbatim}

Вы задали переменную шелла. Чтобы сослаться на переменную (получить значение по
имени), поставьте перед ней доллар. Пример ниже напечатает 42:

\begin{verbatim}
echo $FOO
42
\end{verbatim}

Если выполнить \spverb|printenv|, мы не увидим \spverb|FOO| в выводе. Команда
\spverb|FOO=42| задает переменную шелла, а не среды. Переменные шелла видны
только ему и не наследуются потомками. Проверим это: из текущего шелла запустим
новый и повторим печать.

\begin{verbatim}
sh
echo $FOO
\end{verbatim}

Получим пустую строку, потому что в процессе-потомке такой переменной
нет. Выполните \spverb|exit|, чтобы вернуться в родительский шелл.

Команда \spverb|export| означает, что переменная становится частью
среды. Установленная таким образом, она видна в списке printenv и доступна
потомкам:

\begin{verbatim}
export FOO=42

printenv | grep FOO
FOO=42

sh
echo $FOO
42
\end{verbatim}

Иногда нужно запустить процесс с переменной, но так, чтобы не повлиять на
текущий набор. Тогда команду указывают после выражения \spverb|ИМЯ=значение|:

\begin{verbatim}
BAR=99 printenv | grep BAR
BAR=99
\end{verbatim}

\spverb|Printenv| порождает новый процесс, которому доступна переменная
\spverb|BAR|. Если снова напечатать \spverb|$BAR|, получим пустую строку.

Программы читают из переменных настройки. Например, клиент к базе данных
PostgreSQL различает два десятка переменных: \spverb|PGHOST|,
\spverb|PGDATABASE|, \spverb|PGUSER| и другие. У переменных среды приоритет
выше, чем у параметров \spverb|--host|, \spverb|--user| и аналогов. Если в
текущем шелле выполнить команды:

\begin{verbatim}
export PGHOST=host.com PGDATABASE=project
\end{verbatim}

\noindent
, то каждая утилита из поставки PostgreSQL сработает на заданном сервере и
базе. Это удобно для серии команд: не придется каждый раз указывать
\spverb|--host| и другие аргументы.

\section{Конфигурация в среде}

Переменными среды можно задать конфигурацию. Каждый язык предлагает функции,
чтобы читать переменные в строки или словарь. Разберем плюсы и минусы этого
подхода.

Поскольку окружение находится в памяти, приложение не обращается к диску при их
чтении. Дело даже не в том, что память быстрее диска (человек не отличит сотую
долю секунды от тысячной). Приложение, которое не зависит от файлов более
автономно и потому удобней в поддержке.

Бывает, файл конфигурации оказался в другой папке, и приложение не может его
найти. Или, что еще хуже, запускается со старой версией файла. Это замедляет
команду, оттягивает на себя полезное время.

Передавать пароли и ключи в переменных безопаснее, чем в файлах. Их могут
прочесть другие программы, в том числе вредоносные. По ошибке файл конфигурации
может попасть в репозиторий и остаться в истории. Переменные среды эфемерны: они
живут только памяти операционной системы. Невозможно прочитать переменные
процесса, запущенного другим пользователем.

Индустрия постепенно уходит от файлов в сторону \emph{контейнеров}. Если раньше
мы копировали файлы по FTP, то сегодня приложение запускают из
\emph{образов}. Это своего рода архив, в который упакован код и его окружение. В
отличии от настоящего архива, образ нельзя изменить. Чтобы обновить файл в
образе, придется собрать его заново.

Наоборот, виртуализация лояльна к переменным среды. Их указывают при запуске
образа в параметрах. Один и тот же образ стартует с разными переменными; новая
сборка не требуется. Чем больше опций можно задать переменными, тем удобней
образ в работе. Пример ниже запускает PostgreSQL с готовой базой и
пользователем:

\begin{verbatim}
docker run \
  -e POSTGRES_DB=project_db \
  -e POSTGRES_USER=POSTGRES_USER \
  -e POSTGRES_PASSWORD=project_secret \
  -d postgres
\end{verbatim}

Принцип <<конфигурация в среде>> встречается в одном из пунктов <<The
Twelve-Factor App>>\footurl{https://12factor.net/}. Это свод правил для
разработки надежных приложений. Третий пункт посвящен конфигурации. Автор
перечисляет плюсы переменных среды: независимость от файлов, поддержка на всех
платформах.

\section{Недостатки среды}

Переменные не поддерживают типы: любое значение это текст. Вывод типов остается
на откуп приложения. Делайте это декларативно, а не вручную. Так, в проектах на
Python встречается код:

\begin{verbatim}
db_port = int(os.environ["DB_PORT"])
\end{verbatim}

Когда переменных больше двух, подобный код становится уродливым. Постройте
словарь, где ключ это имя переменной, а значение~--- функция вывода. Специальный
код обходит \spverb|env_mapping| и наполняет словарь результата. Для краткости
опустим перехват ошибок:

\begin{verbatim}
import os
env_mapping = {"DB_PORT": int}

result = {}
for (env, fn) in env_mapping.iteritems():
    result[env] = fn(os.environ[env])
\end{verbatim}

Подход справедлив и для других языков. В Clojure для разбора переменных подойдет
спека.

Переменные среды не работают с иерархией. Это плоский набор ключей и значений,
что не всегда ложиться на конфигурацию. Чем больше параметров, тем чаще их
группируют по смыслу. Предположим, подключение к базе задают задают десять
параметров. Чтобы не ставить перед каждым префикс, их выносят во вложенный
словарь.

\begin{verbatim}
;; so-so
{:db-name "book"
 :db-user "ivan"
 :db-pass "****"}

;; better
{:db {:name "book"
      :user "ivan"
      :pass "****"}}
\end{verbatim}

В различных системах вложенные переменные читают по-разному. Например, одинарное
подчеркивание разделет лексемы, но не меняет структуру. Двойное подчеркивание
означает вложенность:

\begin{verbatim}
"DB_NAME=book"
;; {:db-name "book"}

"DB__NAME=book"
;; {:db {:name "book"}}
\end{verbatim}

Массив задают в квадратных скобках или через запятую. При выводе массивов из
строк остается риск ложного разбиения. Это случается, когда запятая или скобка
относится к слову, а не к синтаксису.

Форматы JSON и YAML задают четкий стандарт, как записывать коллекции. Для
переменных среды нет единого соглашения. Ситуация ухудшается, когда компонент
ожидает параметр большой вложенности, например список словарей. Переменные среды
не могут передать такую структуру.

При разработке становится очевиден еще один недостаток переменных~--- в
некоторых системах они доступны только для чтения. Это верно идеологически, но
вынуждает перезагружать REPL на каждое изменение конфигурации. Для файла
достаточно изменить его и вызвать функцию загрузки.

\subsection{Env-файлы}

Когда переменных среды много, вводить их вручную ввод через \spverb|export|
утомительно. Переменные выносят в файл, который называют
\emph{env}-конфигурацией.

Технически это шелл-скрипт, но чем меньше в нем скриптовых возможностей, тем
лучше. В идеале в файле только пары \spverb|ИМЯ=значение| по одной на каждую
строку.

\begin{verbatim}
DB_NAME=book
DB_USER=ivan
DB_PASS=****
\end{verbatim}

Чтобы считать переменные в шелл, вызывают команду \spverb|source <file>|. Это
одна из команд \spverb|bash|, которая выполняет скрипт в текущем сеансе. Cкрипт
добавит переменные в шелл, и вы увидите их после завершения \spverb|source|. Это
важное отличие от команды \spverb|bash <file>|, которая выполнит скрипт в
отдельном шелле.

\begin{verbatim}
source ENV
echo $DB_NAME
book
\end{verbatim}

Если запустить из текущего шелла приложение, оно \emph{не увидит} переменные из
файла. Вспомним, что выражение \spverb|VAR=value| задает \emph{локальную}
переменную. \spverb|DB_NAME| и другие не попадут в окружение и не будут
наследованы приложением. Проверим это с помощью \spverb|printenv|:

\begin{verbatim}
source ENV
printenv | grep DB
# exit 1
\end{verbatim}

Есть два способа решить проблему. Первый~--- открыть файл и расставить перед
каждой парой выражение \spverb|export|. Тогда \spverb|source| этого файла
добавит переменные в окружение:

\begin{verbatim}
cat ENV
export DB_NAME=book
export DB_USER=ivan
export DB_PASS=****

source ENV
printenv | grep DB
DB_NAME=book
DB_USER=ivan
DB_PASS=****
\end{verbatim}

Недостаток способа в том, что в env-файле появляется дополнительная
логика. Легко забыть \spverb|export| перед переменной, и приложение не прочитает
ее.

Второй способ основан на параметре \spverb|-a| текущего шелла. Когда он
установлен, локальная переменная сама переходит в окружение. Перед тем, как
читать переменные из файла, флаг возводят в истину, а затем снова в ложь.

\begin{verbatim}
set -a
source ENV
printenv | grep DB
# prints all the vars
set +a
\end{verbatim}

Выражение \spverb|set| противоречиво: параметр с минусом включает параметр, а с
плюсом~--- отключает. Это исключение, которое придется запомнить.

Если считать переменную, которая уже была в окружении, она заменит прежнее
значение. Так строят конфигурации на файлах-переопределениях. Если нужны особые
настройки для тестов, не обязательно дублировать весь файл. Создайте файл с
полями, которые нужно заменить и выполнить его после главного.

Пусть тестовые настройки отличаются от стандартных именем базы. Файл
\spverb|ENV| несет в себе базовые параметры. В \spverb|ENV_TEST| поместим новое
значение \spverb|DB_NAME=test|. Затем прочитаем оба файла.

\begin{verbatim}
set -a
source ENV
source ENV_TEST
set +a

echo $DB_NAME
test
\end{verbatim}

Читатель заметит, что env-файлы противоречивы. Мы сказали, что переменные
снимают зависимость от файлов, но в итоге поместили переменные в файл. Какой в
этом смысл?

Разница между JSON- и env-файлами в том, кто их читает. В первом случае
конфигурацию читает приложение, а во втором~--- операционная система. Если
JSON-файл располагается в строго заданной директории, то переменные среды
доступны ото всюду. Тем самым мы выносим из приложения код, который ищет файлы,
и оставляем запас для маневра.

\section{Переменные среды в Clojure}

Clojure это гостевая платформа. Язык не предлагает доступ к системным ресурсам;
нет и функции для чтения переменных среды. Мы получим их из класса
\spverb|java.lang.System|. Импортировать класс не нужно, он доступен в любом
пространстве имен.

Статический метод \spverb|getenv| вернет либо одну переменную по имени, либо
словарь всех переменных, если имя не указали.

\begin{verbatim}
;; a single variable
(System/getenv "HOME")
"/Users/ivan"

;; all variables
(System/getenv)
{"JAVA_ARCH" "x86_64", "LANG" "en_US.UTF-8"} ;; truncated
\end{verbatim}

Во втором случае получили не Clojure-, а Java-коллекцию. Это постоянная версия
\spverb|java.util.Map|. Поэтому переменные нельзя изменить после запуска JVM.

Чтобы облегчить работу со словарем, переведем его к типу Clojure. Заодно
исправим ключи: сейчас это строки в верхнем регистре и подчеркиваниями. В
Clojure пользуются кейвордами и записью <<kebab-case>>: нижний регистр с
дефисами.

Напишем функцию для перевода ключа:

\begin{verbatim}
(require '[clojure.string :as str])

(defn remap-key [^String key]
  (-> key
      str/lower-case
      (str/replace #"_" "-")
      keyword))
\end{verbatim}

\noindent
и убедимся в ее работе:

\begin{verbatim}
(remap-key "DB_PORT")
:db-port
\end{verbatim}

Функция \spverb|remap-env| проходит по Java-коллекции и возвращает ее
<<кложурную>> версию с привычными ключами:

\begin{verbatim}
(defn remap-env [env]
  (reduce
   (fn [acc [k v]]
     (let [key (remap-key k)]
       (assoc acc key v)))
   {}
   env))
\end{verbatim}

Итоговый словарь может оказаться большим. Современные системы хранят в памяти до
сотни переменных. Приведем небольшую часть словаря:

\begin{verbatim}
(remap-env (System/getenv))

{:home "/Users/ivan"
 :lang "en_US.UTF-8"
 :term "xterm-256color"
 :java-arch "x86_64"
 :term-program "iTerm.app"
 :shell "/bin/zsh"}
\end{verbatim}

Теперь когда переменные это обычный словарь, его двигают по тому же
конвейеру. Это вывод типов и валидация спекой. Поскольку все значения строки,
изменим отдельные спеки. Например, доработаем вывод чисел из строк. Удачной
будет спека, которая работает и с числами для JSON, и с текстом для переменных
среды. Вот как выглядит умный парсер числа:

\begin{verbatim}
(s/def ::->int
  (s/conformer
   (fn [value]
     (cond
       (int? value) value
       (string? value)
       (try (Integer/parseInt value)
            (catch Exception e
              ::s/invalid))
       :else ::s/invalid))))
\end{verbatim}

С этой спекой можно менять источник параметров без изменений в коде.

\subsection{Проблема лишних ключей}

У словаря переменных недостаток~--- много посторонних полей. Приложению не нужно
знать домашнюю папку и версию терминала. Эти поля вносят шум при печати или
записи в лог. Если параметры не прошли спеку, мы увидим лишние данные в отчете
\spverb|explain|. Их следует убрать.

На последнем шаге \spverb|s/conform| нужно выбрать из словаря только полезные
ключи. Функция \spverb|select-keys| выбирает подмножество словаря по списку
ключей. Но как задать ключи? Перечислять их вручную долго, и к тому же мы
дублируем код. Мы уже указали ключи в спеке \spverb|::config|. С помощью
небольшого трюка вытащим из нее список ключей.

Функция \spverb|s/form| принимает ключ спеки и возвращает ее <<замороженную>>
форму-список. Каждый элемент списка это примитив Clojure: символ, кейворд,
вектор и так далее. Для спеки \spverb|::config| получим список:

\begin{verbatim}
(clojure.spec.alpha/keys
 :req-un [:book.config/server_port
          :book.config/db
          :book.config/event])
\end{verbatim}

Обратите внимание: это \emph{список}, а не код. Нужные нам ключи это третий
элемент формы, вектор после \spverb|:req-un|. В общем случае следует учесть и
другие типы ключей, например \spverb|:opt-un|. Напишем универсальную функцию,
которая вернет все ключи из спеки \spverb|s/keys|.

Отбросим первый символ формы. Останется список, где нечетные элементы это тип
ключа, а четные~--- их вектор. Перестроим список в словарь и объединим
значения. Для ключей \spverb|-un| отбросим пространство, потому что в
конфигурации ими не пользуются. Все вместе дает нам следующую функцию:

\begin{verbatim}
(defn spec->keys
  [spec-keys]
  (let [form (s/form spec-keys)
        params (apply hash-map (rest form))
        {:keys [req opt req-un opt-un]} params
        ->unqualify (comp keyword name)]
    (concat req
            opt
            (map ->unqualify opt-un)
            (map ->unqualify req-un))))
\end{verbatim}

\noindent
Проверим спеку загрузчика:

\begin{verbatim}
(spec->keys ::config)
(:server_port :db :event)
\end{verbatim}

\noindent
Перепишем чтение переменных в словарь:

\begin{verbatim}
(defn read-env-vars []
  (let [cfg-keys (spec->keys ::config)]
    (-> (System/getenv)
        (remap-env)
        (select-keys cfg-keys))))
\end{verbatim}

\noindent
Так мы получим только полезные ключи, то есть те, что указаны в спеке.

\subsection{Загрузчик среды}

Доработаем загрузчик так, чтобы он работал с переменными среды. Замените первые
два шага на \spverb|read-env-vars|:

\begin{verbatim}
(defn load-config! []
  (-> (read-env-vars)
      (coerce-config)
      (set-config!)))
\end{verbatim}

Сделайте так, чтобы источник данных можно было задать параметром. Например,
\spverb|:source "/path/to/config.json"| означает считать файл, а
\spverb|:source :env|~--- переменные среды.

Еще сложнее: как считать оба источника и объединить их? Важен ли порядок и как
его обеспечить? Как сделать объединение ассиметричным, то есть когда левый
словарь только \emph{замещает} поля правого, но не дополняет его новыми?

\subsection{Вывод структуры}

Мы упоминали проблему структуры в переменных среды. Редко бывает так, что
конфигурация это плоский словарь. Близкие параметры собирают во вложенные
словари, например отдельно параметры сервера и базы данных. Сгруппированные
настройки удобней в поддержке.

Улучшим загрузчик: научим его читать вложенные переменные. Договоримся, что
двойное подчеркивание означает вложенную структуру. Поместим в файл
\spverb|ENV_NEST| следующие переменные:

\begin{verbatim}
DB__NAME=book
DB__USER=ivan
DB__PASS=****
HTTP__PORT=8080
HTTP__HOST=api.random.com
\end{verbatim}

\noindent
Прочитаем его и запустим REPL с новой средой:

\begin{verbatim}
set -a
source ENV_NEST
lein repl
\end{verbatim}

Изменим функции раздора ключа и обхода окружения. Функция
\spverb|remap-key-nest| принимает ключ-строку и возвращает вектор составных
частей:

\begin{verbatim}
(def ->keywords (partial map keyword))

(defn remap-key-nest
  [^String key]
  (-> key
      str/lower-case
      (str/replace #"_" "-")
      (str/split #"--")
      ->keywords))

(remap-key-nest "DB__PORT")
;; (:db :port)
\end{verbatim}

Новая функция обхода отличается тем, что делает не \spverb|assoc|, а
\spverb|assoc-in|, что порождает вложенность:

\begin{verbatim}
(defn remap-env-nest
  [env]
  (reduce
   (fn [acc [k v]]
     (let [key-path (remap-key-nest k)]
       (assoc-in acc key-path v)))
   {}
   env))
\end{verbatim}

Код ниже вернет сгруппированные параметры, как и задумано. Для удобства приведем
его подмножество:

\begin{verbatim}
(-> (System/getenv)
    (remap-env-nest)
    (select-keys [:db :http]))

{:db {:user "ivan", :pass "****", :name "book"},
 :http {:port "8080", :host "api.random.com"}}
\end{verbatim}

\noindent
Дальше действуем как обычно: пишем спеку, выводим типы из строк и так далее.

Подумайте, как задать переменными среды массив. Как считать его из строки? В
каких случаях возможно ложное разбиение?

\section{Простой менеджер конфигурации}

В этот момент читатель решит, что конфигурация в файле это плохая идея. Однако
не бросайтесь все переписывать на переменные среды. На практике работают с
\emph{гибридными} моделями, которые сочетают оба подхода. Приложение читает
основные параметры из файла, а пароли и ключи доступа из среды.

Рассмотрим несколько способов подружить файлы и окружение. Наивное решение не
потребует писать код: оно работает на утилитах командной строки. Программа
\spverb|envsubst| из пакета \spverb|GNU gettext| это простая шаблонная
система. Чтобы установить \spverb|gettext|, выполните в терминале команду

\begin{verbatim}
<manager> install gettext
\end{verbatim}

\noindent
, где \spverb|<manager>| это менеджер пакетов вашей системы (\spverb|brew|,
\spverb|apt|, \spverb|yum| и другие).

Текст шаблона приходит из \spverb|stdin|, а роль контекста играют переменные
среды. Утилита заменяет выражения \spverb|$VAR_NAME| на значения одноименной
переменной. Напишем шаблон конфигурации:

\begin{verbatim}
cat config.template.json
{
    "server_port": $HTTP_PORT,
    "db": {
        "dbtype":   "mysql",
        "dbname":   "$DB_NAME",
        "user":     "$DB_USER",
        "password": "$DB_PASS"
    },
    "event": [
        "$EVENT_START",
        "$EVENT_END"
    ]
}
\end{verbatim}

\noindent
Поместим переменные в отдельный файл:

\begin{verbatim}
cat ENV_TPL

DB_NAME=book
DB_USER=ivan
DB_PASS='*(&fd}A53z#$!'
HTTP_PORT=8080
EVENT_START='2019-07-05T12:00:00'
EVENT_END='2019-07-12T23:59:59'
\end{verbatim}

\noindent
Считаем переменные и <<отрендерим>> шаблон:

\begin{verbatim}
source ENV_TPL
cat config.template.json | envsubst
{
    "server_port": 8080,
    "db": {
        "dbtype":   "mysql",
        "dbname":   "book",
        "user":     "ivan",
        "password": "*(&fd}A53z#$!"
    },
    "event": [
        "2019-07-05T12:00:00",
        "2019-07-12T23:59:59"
    ]
}
\end{verbatim}

\noindent
Чтобы записать результат в файл, добавьте в конец оператор вывода:

\begin{verbatim}
cat config.template.json | envsubst > config.ready.json
\end{verbatim}

Способ с \spverb|envsubst| на первый взгляд примитивен, но полезен на
практике. Принцип шаблона снимает вопрос о структуре данных. Переменные хранят
как плоский словарь и подставляют в нужное место; не приходится думать о
вложенности.

Иногда приложение требует несколько файлов конфигурации. Если это веб-сервер,
один и тот же порт указывают в настройках приложения и Nginx. Из других служб
отметим iptables, sendmail и crontab. Они тоже опираются на параметры приложения
(IP-адреса, белые хосты). На базе одних и тех же переменных и шаблонов мы
получим файлы для всех сервисов. Останется разложить их по нужным папкам и
перезагрузить службы.

Утилита \spverb|envsubst| становится менеджером конфигураций. Чтобы
автоматизировать процесс, добавьте скрипт, который пробегает по шаблонам и
<<рендерит>> их переменными. Решение не тянет на промышленный уровень, но
подойдет для простых проектов.

\section{Чтение среды из конфигурации}

Следующие техники делают так, что приложение читает параметры
\emph{одновременно} из файла и среды. Разница в том, на каком шаге это
происходит.

Предположим, основные параметры записаны в файле, а пароль базы данных приходит
из среды. Договоримся с членами команды, что поле \spverb|:password| в файле
содержит не пароль, а имя переменной, например \spverb|"DB_PASS"|. Напишем
спеку, которая выводит значение переменной по ее имени:

\begin{verbatim}
(s/def ::->env
  (s/conformer
   (fn [varname]
     (or (System/getenv varname)
         ::s/invalid))))
\end{verbatim}

Если переменную не задали, вывод вернет ошибку. Прежде чем подключатся к базе,
отбросим пустые символы по краям и убедимся, что строка не пустая.

\begin{verbatim}
(s/def ::db-password
  (s/and ::->env
         string?
         (s/conformer str/trim)
         not-empty))
\end{verbatim}

\noindent
Быстрая проверка. Запустим REPL с переменной \spverb|DB_PASS| и
прочитаем ее спекой:

\begin{verbatim}
DB_PASS='*(&fd}A53z#$!' lein repl

(s/conform ::db-password "DB_PASS")
"*(&fd}A53z#$!"
\end{verbatim}

Чтобы вынести параметр из файла в переменную, замените его значение на имя
переменной. Обновите спеку этого поля: добавьте \spverb|::->env| в начало
цепочки \spverb|s/and|.

Другой способ прочитать переменные из файла~--- расширить его \emph{тегами}. Тег
это короткое слово, которое указывает, что значение за ним читают особым
образом. Форматы YAML и EDN поддерживают теги. Библиотеки для них предлагают
несколько основных тегов. В основном это вывод сложных типов из скаляров.

Рассмотрим пример с EDN: тег начинается со знака решетки и захватывает следующее
значение. Например, \spverb|#inst "2019-07-10"| выводит дату из строки.

Тег связан с функцией одного аргумента, которая вычисляет новое значение на базе
исходного. Чтобы задать свой тег, в функцию \spverb|clojure.edn/read-string|
передают словарь тегов. Ключи словаря это символы тегов, значения~--- функции.

Добавим тег \spverb|#env|, который вернет значение переменной по ее имени. Имя
переменной может быть строкой или символом. Определим функцию:

\begin{verbatim}
(defn tag-env
  [varname]
  (cond
    (symbol? varname)
    (System/getenv (name varname))
    (string? varname)
    (System/getenv varname)
    :else
    (throw (new Exception "wrong var type"))))
\end{verbatim}

\noindent
Прочитаем edn-строку с новым тегом:

\begin{verbatim}
(require '[clojure.edn :as edn])

(edn/read-string
 {:readers {'env tag-env}}
 "{:db-password #env DB_PASS}")

;; {:db-password "*(&fd}A53z#$!"}
\end{verbatim}

Чтобы не передавать теги каждый раз, объявим \spverb|partial| от
\spverb|edn/read-string|. Получим функцию, которая принимает только текст:

\begin{verbatim}
(def read-config
  (partial edn/read-string
           {:readers {'env tag-env}}))
\end{verbatim}

Чтобы прочесть edn-файл с особыми тегами, считайте его содержимое в строку и
передайте в \spverb|read-config|:

\begin{verbatim}
(-> "/path/to/config.edn"
    slurp
    read-config)
\end{verbatim}

YAML устроен похожим образом: его стандарт предусматривает теги. Тег начинается
с одного или двух восклицательных знаков в зависимости от семантики. Стандартные
теги идут с одним знаком, сторонние с двумя, чтобы отличить их от
стандартных. Этому соглашению следуют не всегда.

Библиотека \spverb|yummy| это парсер YAML-файлов, <<заряженный>> полезными
тегами. Среди прочих он предлагает \spverb|!envvar|. Тег возвращает значение
переменной по ее имени. Опишем конфигурацию в файле \spverb|config.yaml|:

\begin{verbatim}
server_port: 8080
db:
  dbtype:   mysql
  dbname:   book
  user:     !envvar DB_USER
  password: !envvar DB_PASS
\end{verbatim}

Подключим библиотеку и прочтем конфигурацию. На месте тегов получим значения
среды:

\begin{verbatim}
(require '[yummy.config :as yummy])
(yummy/load-config {:path "config.yaml"})

{:server_port 8080
 :db {:dbtype "mysql"
      :dbname "book"
      :user "ivan"
      :password "*(&fd}A53z#$!"}}
\end{verbatim}

Мы подробно рассмотрим \spverb|yummy| в другом разделе главы.

Теги несут преимущества и недостатки. С одной стороны, они делают конфигурацию
плотнее: на строку с тегом приходится больше смысла. Запись \spverb|#env DB_PASS|
выглядит короче и потому \emph{приятнее} глазу. Сложные теги выносят в библиотеки,
которые подключают одной строчкой.

С другой стороны, теги привязывают конфигурацию к одной платформе. Если в
YAML-файле встречается тег \spverb|!envvar|, то библиотека на Python не сможет
его прочитать: в ней нет такого тега! Технически это можно исправить:
игнорировать незнакомые теги или установить заглушку. Но подход и не гарантирует
одинаковый результат на разных платформах.

Другой минус тегов в том, что с ними конфигурация обрастает побочными
эффектами. В терминах функционального программирования она теряет
\emph{чистоту}. Появляется соблазн вынести в тег слишком много логики. Например,
импортировать дочерние файлы или форматировать строки. Теги стирают грань между
чтением конфигурации и ее обработкой. Когда тегов слишком много, конфигурацию
трудно поддерживать.

Оба приема~--- разбор спекой и тегами~--- оппонируют друг другу. Делайте так,
как удобно команде и рабочему процессу.

\section{Короткий обзор форматов}

Мы упомянули три формата данных: JSON, EDN и YAML. Перечислим особенности
каждого из них. Наша цель не выявить идеальный формат, а подготовить читателя к
неочевидным моментам, которые возникнут в работе с этими файлами.

\subsection{JSON}

Наверняка JSON известен даже тем, кто не работает с вебом. Это запись данных по
образу JavaScript. Стандарт задает числа, строки, логический тип, null и две
коллекции: массив и словарь. Коллекции могут быть произвольно вложены друг в
друга.

Ключевое преимущество JSON в его популярности. Это стандарт для обмена данными
между клиентом и сервером. По сравнению с XML его легче читать и
поддерживать. JSON поддерживают все современные редакторы, языки и
платформы. Это естественный способ хранить данные в мире JavaScript.

В JSON не предусмотрены комментарии. На первый взгляд это кажется мелочью, но на
практике комментарии важны. Если разработчик добавил новый параметр, хорошим
тоном будет написать комментарий о том, что он делает и какие значения
принимает. Посмотрите конфигурации Redis, PostgreSQL или Nginx~--- более
половины текста это комментарии.

Разработчики придумали уловки, чтобы обойти это ограничение. Например, поставить
одноименное поле перед тем, к которому относится комментарий.

\begin{verbatim}
{
    "server_port": "A port which the web-server is bound to.",
    "server_port": 8080
}
\end{verbatim}

Расчет сделан на то, что библиотека обходит поля по очереди, и второе поле
заменит первое. Стандарт JSON ничего об этом не говорит; двойное поле остается
на откуп разработчика. Логика библиотеки может быть иной, например бросить
исключение или пропустить ключ, который уже в словаре.

Иногда в JSON добавляют комментарии на уровне продукта. Редактор Sublime Text
хранит настройки в .json-файлах с поддержкой JavaScript-комментариев (двойная
косая черта). Но в общем случае у проблемы нет решения.

JSON выгодно отличается от многословного XML, которому он пришел на
замену. Данные в JSON выглядят чище и удобнее, чем дерево XML-тегов. Но
современные форматы выражают данные еще чище. Например, в YAML любую структуру
можно записать без единой скобки за счет отступов.

Синтаксис JSON <<шумит>>: он требует кавычек, двоеточий и запятых там, где
другие форматы лояльны. Например, запятая в конце массива или объекта считается
ошибкой. Ключи словаря не могут быть числами; нет токенов (кейвордов). Нельзя
записать текст в несколько строк.

Сравните данные в YAML и JSON:

%% TODO table

\begin{verbatim}
                              {
server_port: 8080                  "server_port": 8080,
db:                                "db": {
  dbtype:   mysql                      "dbtype":   "mysql",
  dbname:   book                       "dbname":   "book",
  user:     user                       "user":     "ivan",
  password: '****'                     "password": "****"
event:                             },
  - 2019-07-05T12:00:00            "event": [
  - 2019-07-12T23:59:59                "2019-07-05T12:00:00",
                                       "2019-07-12T23:59:59"
                                   ]
                               }
\end{verbatim}

Стандарт JSON не поддерживает теги, о которых мы говорили выше.

\subsection{YAML}

Язык разметки YAML, как и JSON, различает базовые типы: скаляры, null и
коллекции. YAML делает ставку на краткость записи: вложенность задают отступы, а
не скобки. Запятые не обязательны там, где язык выводит их логически. Например,
массив чисел в одну строку выглядит как в JSON:

\begin{verbatim}
numbers: [1, 2, 3]
\end{verbatim}

При записи в столбик скобки и запятые отпадают:

\begin{verbatim}
numbers:
  - 1
  - 2
  - 3
\end{verbatim}

YAML поддерживает комментарии в стиле Python (решетка). За это формат любят
DevOps-инженеры. Программы вроде Docker-compose и Kubernetes работают на
YAML-файлах.

В отличии от JSON, YAML предлагает синтаксис для многострочного текста. Его
проще читать и копировать, чем одну строку с вкраплениями \spverb|\n|.

\begin{verbatim}
description: |
  To resolve the error, please do the following:

  - Press Control + Alt + Delete;
  - Turn off your computer;
  - Walk for a while.

  Then try again.
\end{verbatim}

Язык официально поддерживает теги.

Недостатки YAML вытекают из его преимуществ. Вложенность отступами кажется
удачным решением до тех пор, пока файл не станет большим. Рано или поздно глаз
начнет прыгать на большие расстояния, чтобы сверять уровни структур. Иногда
часть данных <<съезжает>> из-за лишнего отступа. С точки зрения языка ошибки
нет, поэтому ее трудно найти.

В особых случаях отказ от кавычек приводит к неверным типам или
структуре. Предположим, в поле \spverb|phrases| перечислены фразы, которые
увидит пользователь:

\begin{verbatim}
phrases:
  - Welcome aboard!
  - See you soon!
  - Warning: wrong email address.
\end{verbatim}

Из-за двоеточия в последней строке парсер решит, что это вложенный словарь. В
результате получим неверную структуру:

\begin{verbatim}
{:phrases
 ["Welcome aboard!"
  "See you soon!"
  {:Warning "wrong email address."}]}
\end{verbatim}

Другие примеры: версия продукта \spverb|3.3| это число, но \spverb|3.3.1|~---
строка. Телефон \spverb|+79625241745| это число, потому что знак плюса считается
унарным оператором по аналогии с минусом. Лидирующие нули означают восьмеричную
запись: если не добавить кавычки к \spverb|000042|, получится число \spverb|34|.

Это не значит, что YAML неудачный формат. Случаи выше описаны в стандарте и
имеют логическое объяснение. Иногда язык ведет себя не так, как вы ожидали~---
это плата за упрощенный синтаксис.

%% --------------

\subsection{EDN}

Формат EDN занимает особое место в обзоре. Он хранит данные Clojure и потому
играет такую же роль в языке, как JSON в JavaScript. Это \emph{естественный,
  родной} способ связать данные с файлом в Clojure.

Синтаксис EDN почти целиком аналогичен компилятору Clojure. Формат охватывает
больше типов, чем JSON и YAML. Например, из скалярных типов доступны символы и
кейворды (типы \spverb|Symbol| и \spverb|Keyword| из
\spverb|clojure.lang|). Кроме вектора и словаря доступны списки и множества.

Теги начинаются с символа решетки. По умолчанию стандарт предлагает два тега:
\spverb|#inst| и \spverb|#uuid|. Первый читает строку в дату, а второй в
экземпляр \spverb|java.util.UUID|. Идентификаторы используют в распределенных
системах, например БД Cassandra. Выше мы показали, как добавить свой тег: нужно
связать его с функцией одного аргумента при чтении строки.

Пример-комбо с разными типами, коллекциями и тегами:

\begin{verbatim}
{:task-state #{:pending :in-progress :done}
 :account-ids [1001 1002 1003]
 :server {:host "127.0.0.1" :port 8080}
 :date-range [#inst "2019-07-01" #inst "2019-07-31"]
 :cassandra-id #uuid "26577362-902e-49e3-83fb-9106be7f60e1"}
\end{verbatim}

EDN-данные не отличаются от кода. Если скопировать данные в REPL или модуль,
компилятор выполнит их. И наоборот: вывод REPL можно скопировать в файл для
дальнейшей работы.

Функция \spverb|pr-str| переводит данные в текст. Сброс в EDN сводится к простым
шагам: <<напечатать>> данные в строку и записать ее в файл. В примере ниже
результат \spverb|get-huge-dataset| пишут в файл \spverb|dataset.edn|:

\begin{verbatim}
(-> (get-huge-dataset)
    pr-str
    (as-> dump
        (spit "dataset.edn" dump)))
\end{verbatim}

EDN поддерживает не только обычные комментарии. Тег \spverb|#_| игнорирует
следующий за ним элемент. Элементом может быть что угодно, в том числе
коллекция. Если нужно <<заигнорить>> словарь, который занимает несколько строк,
поставьте перед ним \spverb|#_|, и парсер пропустит его.

Это позволяет отключить целые части конфигурации. В следующем примере мы
игнорируем пользователя Ioann. Если поставить обычный комментарий (точка с
запятой), он заденет закрывающие скобки, и выражение не будет закончено.

\begin{verbatim}
{:users [{:id 1 :name "Ivan"}
         {:id 2 :name "Juan"}
         #_{:id 3 :name "Ioann"}]}
\end{verbatim}

EDN это удачный выбор, когда и бекенд, и фронтенд работают на едином стеке
Clojure и ClojureScript.

EDN привязан к Clojure и потому не известен разработчикам на других
языках. Современные редакторы не подсвечивают его синтаксис без сторонних
плагинов. Это может доставить проблем коллегам DevOps, которые работают только с
JSON и YAML. Если к конфигурации обращаются скрипты на Python или Ruby, придется
ставить библиотеку для работы с этим форматом. EDN выбирают там, где Clojure
преобладает над другими технологиями.

\section{Промышленные решения}

Разработчику важно понимать, как работает конфигурация, но мы не ожидаем, что в
каждом проекте ее пишут с нуля. Идеи из этой главы выражены в сторонних
решениях. В последнем разделе мы рассмотрим, какие библиотеки предлагает
сообщество для конфигурации проектов.

Мы остановили внимание на проектах \spverb|cprop|, \spverb|aero| и
\spverb|yummy|. Библиотеки отличаются в идеологии и архитектуре. Мы специально
подобрали их так, чтобы увидеть проблему с разных сторон.

\subsection{Cprop}

%% ----------------

Библиотека \spverb|cprop|\footurl{https://github.com/tolitius/cprop} работает по
принципу <<данные отовсюду>>. В отличии от нашего загрузчика у cprop больше
источников данных. Библиотека читает не только файл и переменные среды, но и
ресурсы, property-файлы и пользовательские словари.

Cprop приводит данные из разных источников к одному виду. Можно сказать, что это
обертка над различными "бекендами" данных. При помощи cprop можно задать
конфигурацию разным способом, но получить одинаковый результат.

В библиотеке задан порядок обхода источников и их приоритет. Параметры из одного
источника могут заменять другие. Например, переменная среды имеет более высокий
приоритет, чем одноименная из файла. Для частных случаев в cprop легко задать
свой порядок загрузки.

На верхнем уровне доступна функция \spverb|load-config|. Вызванная без параметров,
функция запускает стандартный загрузчик. По умолчанию cprop ищет два источника
данных: Java-ресурс и property-файл.

Под ресурсом понимают особый файл, который становится частью Java-проекта. На
этапе разработки файлы ресурсов хранят в директории resources. Cprop ожидает
ресурс с именем \spverb|config.edn|. Это первичный источник данных.

Если системное свойство conf не пустое, библиотека полагает, что это путь к
property-файлу и загружает из него словарь. Системные свойства это особая группа
переменных в рамках Java-машины. Можно сказать, что свойства это аналог среды
окружения для JVM.

При загрузке JVM получает набор свойств по умолчанию, например, тип и версию
операционной системы, параметры файловой системы, опции JVM и
другие. Дополнительные свойства передают параметром \spverb|-D| при запуске
java-программы. Пример ниже запускает скомпилированный jar-файл со свойством
\spverb|conf|:

\begin{verbatim}
java -Dconf="/path/to/config.properties" -jar project.jar
\end{verbatim}

Файлы \spverb|.properties| это пары \spverb|поле=значение| по одной на строку. Поля носят
доменную структуру: это лексемы, разделенные точкой. Лексемы убывают по
старшинству:

\begin{verbatim}
db.type=mysql
db.host=127.0.0.1
db.pool.connections=8
\end{verbatim}

Библиотека расценивает точки как вложенные словари. Загрузка такого файла вернет
структуру:

\begin{verbatim}
{:db {:type "mysql"
      :host "127.0.0.1"
      :pool {:connections 8}}}
\end{verbatim}

Получив конфигурацию из ресурса или property-файла, cprop ищет переопределения в
переменных среды. Для них работают те же правила, что и в нашем
загрузчике. Например, переменная \spverb|DB__POOL__CONNECTIONS=16| заменит
значение 8 в структуре выше. При этом cprop игнорирует переменные, которые не
входят в ключи конфигурации, и тем самым не загрязняет~ее.

Стандартные пути поиска можно задать ключами:

\begin{verbatim}
(load-config
 :resource "private/config.edn"
 :file "/path/custom/config.edn")
\end{verbatim}

Для работы с ресурсами cprop предлагает функции из модуля
\spverb|cprop.source|. Например, from-env Вернет словарь всех переменных среды,
\spverb|from-props-file| загрузит properties-файл и так далее. Разработчик вправе
построить их них такую комбинацию, которая нужна проекту.

Ключ \spverb|:merge| объединяет конфигурацию с произвольными источниками. Убер-пример
из документации:

\begin{verbatim}
(load-config
 :resource "path/within/classpath/to.edn"
 :file "/path/to/some.edn"
 :merge [{:datomic {:url "foo.bar"}}
         (from-file "/path/to/another.edn")
         (from-resource "path/within/classpath/to-another.edn")
         (from-props-file "/path/to/some.properties")
         (from-system-props)
         (from-env)])
\end{verbatim}

В вектор \spverb|:merge| можно добавить любое выражение, которое вернет словарь. Но в
общем случае \spverb|(load-config)| без параметров будет достаточным решением.

Чтобы отследить загрузку такой сложной конфигурации, установите переменную среды
DEBUG:

\begin{verbatim}
export DEBUG=y
\end{verbatim}

С ней cprop выводит в лог служебную информацию. Это список источников, порядок
их загрузки, переопределение переменных и так далее.

Cprop только загружает данные из разных источников, но не проверяет их. В
библиотеке не реализована валидация спекой, как это сделано в нашем
загрузчике. Этот шаг остается на усмотрение разработчика.

В cprop работает своя система вывода типов. Если строка состоит только из цифр,
ее приводят к числу. Значения с запятыми становятся списками. В целом этих
возможностей недостаточно для полного контроля за типами. Вам по-прежнему
понадобится \spverb|spec| и \spverb|conform| для вывода типов и сообщений об ошибке.

\subsection{Aero}

Проект aero предлагает другой подход. В отличии от cprop, библиотека работает
только с одним источником данных: файлами \spverb|*.edn|. Aero несет на борту теги, с
которыми EDN становится похож на мини-язык программирования. В нем появляются
базовые операторы ветвления, импорта, форматирования. По-другому это можно
назвать "EDN на стероидах".

Функция \spverb|read-config| читает файл или ресурс EDN:

\begin{verbatim}
(require '[aero.core :refer (read-config)])

(read-config "config.edn")
(read-config (clojure.java.io/resource "config.edn"))
\end{verbatim}

Центральная точка библиотеки это теги, которые срабатывают при чтении. Разберем
основные из них. Тег \spverb|#env| заменяет имя переменной среды на ее значение. Нам
уже приходилось писать его:

\begin{verbatim}
{:db {:passwod #env DB_PASS}}
\end{verbatim}

Тег \spverb|#envf| форматирует строку переменными среды. Например, параметры базы
данных заданы отдельными полями, но вы предпочитаете JDBC URI -- длинную строку,
похожую на веб-адрес. Чтобы не дублировать данные, адрес вычисляют на базе
исходных полей:

\begin{verbatim}
{:db-uri #envf ["jdbc:postgresql://%s/%s?user=%s"
                DB_HOST DB_NAME DB_USER]}
\end{verbatim}

Тег \spverb|#or| похож на одноименный оператор Clojure и в основном нужен для значений
по умолчанию. Так, в нашем env-файле не задан порт базы данных. Чтобы избежать
неочевидности, укажем стандартный порт:

\begin{verbatim}
{:db {:port #or [#env DB_PORT 5432]}}
\end{verbatim}

Оператор \spverb|#profile| предлагает интересный способ взять значение в зависимости от
профиля. Значение за тегом обязательно словарь. Ключи словаря это профили, а
значения -- то, что получим в результате его разрешения. Профиль задают в
параметрах \spverb|read-config|.

Пример ниже показывает, как определить имя базы данных по профилю. Без профиля
мы получим \spverb|"book"|, но для \spverb|:test| имя станет \spverb|"book_TEST"|:

\begin{verbatim}
{:db {:name #profile {:default "book"
                      :dev     "book_DEV"
                      :test    "book_TEST"}}}

(read-config "aero.test.edn" {:profile :test})
{:db {:name "book_TEST"}}
\end{verbatim}

Тег \spverb|#include| внедряет в конфигурацию содержимое другого edn-файла. При этом
дочерний файл тоже содержит aero-теги, и библиотека выполняет их рекурсивно. К
импорту прибегают, когда конфигурация отдельных компонентов становится большой.

\begin{verbatim}
{:queue #include "message-queue.edn"}
\end{verbatim}

Тег \spverb|#ref| описывает ссылку на любое место конфигурации. Это вектор ключей,
который обычно передают в \spverb|get-in|. Ссылка позволяет избежать дублирования
данных. Например, сторонний компонент нуждается в пользователе, под которым мы
подключаемся к базе данных:

\begin{verbatim}
;; config.edn
{:db {:user #env DB_USER}
 :worker {:user #ref [:db :user]}}

;; result
{:db {:user "ivan"}, :worker {:user "ivan"}}
\end{verbatim}

Эти и другие теги можно комбинировать различными способами и добавлять
собственные.

Фактически aero предлагает несложный язык описания конфигураций. Проект выглядит
многообещающе. Aero подкупает интересными идеями и красотой реализации. Но в
момент, когда вам захочется переехать с негибкого JSON на aero, подумайте об
обратной стороне медали.

Конфигурацию не случайно отделяют от кода. Если бы не потребность индустрии, мы
бы хранили параметры в исходных файлах. Но все же мы не делаем так, -- наоборот,
хорошие практики в один голос советуют отделять параметры от кода. В том числе
потому, что в отличие от кода конфигурация декларативна.

Негибкие JSON- и properties-файлы обладают важным свойством: они
декларативны. Когда вы открыли файл в редакторе или "катнули" его в консоль, то
сразу видите данные. Отдельные их части могут дублироваться, синтаксис не
настолько удобен для чтения. Но данные выражаются сами в себя, и ошибки быть не
может. Вы просто видите их.

Наоборот, файл с нестандартными тегами труден в поддержке. Это не конфигурация,
а код. Файл становится трудно читать, его нужно выполнить. При чтении файла у
вас в голове запускается мини-интерпретатор, который не гарантирует точный
результат.

Получается своего рода круг. Мы вынесли параметры в статическую конфигурацию,
добавили теги и вернулись к коду. Такой подход тоже имеет право на жизнь, но к
нему следует прийти осознанно.

\subsection{Yummy}

Библиотека yummy замыкает наш обзор. Yummy отличается от аналогов двумя
свойствами. Во-первых, она работает с файлами YAML для чтения конфигурации
(отсюда и название). Во-вторых, процесс ее загрузки максимально похож на тот,
что мы рассмотрели в начале главы.

Вспомним, что полноценный загрузчик не только читает параметры. Цикл
конфигурации включает проверку данных и вывод ошибки. Сообщение об ошибке внятно
объясняет, в чем причина. С помощью необязательных параметров мы должны иметь
возможность "зацепиться" за основные события. Yummy предлагает почти все из
перечисленного.

Библиотека читает YAML-разметку из файла. Путь к файлу либо передан в
параметрах, либо библиотека ищет его по определенному имени в переменных среды
или свойствах JVM.

Вариант, когда путь задан явно:

\begin{verbatim}
(require '[yummy.config :refer [load-config]])

(load-config {:path "config.yaml"})
\end{verbatim}

Во втором примере вместо пути задали имя программы. Yummy ищет путь к файлу в
переменной среды \spverb|<name>_CONFIGURATION| или свойстве \spverb|<name>.configuration|:

\begin{verbatim}
export BOOK_CONFIGURATION=config.yaml
\end{verbatim}

\begin{verbatim}
(load-config {:program-name :book})
\end{verbatim}

Библиотека расширяет YAML несколькими тегами. Это знакомый вам \spverb|!envvar| для
переменных среды:

\begin{verbatim}
db:
  password: !envvar DB_PASS
\end{verbatim}

Тег \spverb|!keyword| полезен в случаях, когда вместо строки ожидают кейворд:

\begin{verbatim}
states:
  - !keyword task/pending
  - !keyword task/in-progress
  - !keyword task/done
\end{verbatim}

Результат:

\begin{verbatim}
{:states [:task/pending :task/in-progress :task/done]}
\end{verbatim}

Тег \spverb|!uuid| аналогичен \spverb|#uuid| для EDN. Он возвращает объект
\spverb|java.util.UUID| из строки:

\begin{verbatim}
system-user: !uuid cb7aa305-997c-4d53-a61a-38e0d8628dbb
\end{verbatim}

Тег \spverb|!slurp| читает текст из стороннего файла. Это полезно для сертификатов
шифрования. Их содержимое это длинная строка, которая плохо укладываются в общую
конфигурацию:

\begin{verbatim}
tls:
  auth: !slurp "certs/ca.pem"
  cert: !slurp "certs/cert.pem"
  pkey: !slurp "certs/key.pk8"
\end{verbatim}

Если в директории certs оказались все нужные сертификаты, в ключах \spverb|:auth|,
\spverb|:cert| и \spverb|:pkey| будет содержимое этих файлов.

Чтобы проверить данные, в параметры \spverb|load-config| передают ключ спеки. Когда
ключ указан, yummy выполняет s/assert для параметров из yaml-файла. Если данные
не прошли проверку, всплывает исключение. Yummy использует библиотеку expound,
чтобы улучшить отчет спеки об ошибке.

\begin{verbatim}
(load-config {:program-name :book
              :spec ::config})
\end{verbatim}

Словарь опцией yummy принимает параметр \spverb|:die-fn|. Это функция, которая будет
вызвана, если любая из стадий завершится с ошибкой. Функция принимает два
аргумента: объект исключения и текстовую метку, которая подсказывает, на какой
стадии произошла ошибка.

Если \spverb|:die-fn| не задан, yummy вызывает обработчик по умолчанию. Обработчик
выводит текст в stderr и завершает программу со кодом 1. Вспомним, что это
неудобно на этапе разработки: мы не хотим обрывать REPL из-за ошибки в
конфигурации. В интерактивном сеансе die-fn подавляет исключение и только
выводит текст:

\begin{verbatim}
(load-config
 {:program-name :book
  :spec ::config
  :die-fn (fn [e msg]
            (binding [*out* *err*]
              (println msg)
              (println (ex-message e))))})
\end{verbatim}

Но в боевом режиме мы запишем исключение в лог и завершим программу.

\begin{verbatim}
(load-config
 {:program-name :book
  :spec ::config
  :die-fn (fn [e msg]
            (log/errorf e "Config error")
            (System/exit 1))})
\end{verbatim}

Из недостатков yummy отметим, что для работы со спекой используется
\spverb|s/assert|. Функция не выводит новые значения, как это делает
\spverb|s/conform|, а только выбрасывает исключение, если проверка не
прошла. Поэтому эффект conform-спек не окажет действия на данные, которые вы
получите.

С другой стороны, это сделано нарочно. Библиотеку писали так, что вывод типов
срабатывает на этапе тегов, а спека только проверяет данные. С таким подходом
все преобразования видны на уровне yaml-файла.

\section{Заключение}

Перечислим основные тезисы из разделов этой главы.

Конфигурация нужна для того, чтобы помочь приложению пройти стадии
производства. На каждой стадии приложение запускают с особыми настройками, чтобы
выполнить должные проверки. Без поддержки конфигурации это просто невозможно.

Загрузка конфигурации включает в себя чтение данных, вывод типов и проверку
значений. В случае ошибки программа выводит понятное сообщение и завершается с
аварийным кодом. Нет смысла продолжать работу с неверными параметрами.

Источником конфигурации может быть файл, Java-ресурс, переменные
окружения. Допустимы гибридные схемы, когда основные данные приходят из файла, а
секретные поля из окружения.

Переменные среды живут в памяти операционной системы. Если переменных много, их
помещают в ENV-файл. Но приложение не читает этот файл; его загружает в память
система, которая отвечает за работу приложения на сервере. С точки зрения
приложения неизвестно, откуда пришли переменные.

Окружение это плоский словарь. Переменные хранят только текст, ключи никак не
структурированы. В разных системах прибегают к особым соглашениям о том, как имя
переменной ложится на логическую структуру. Это могут быть точки, двойные
подчеркивания или что-то другие.

Форматы файлов различаются синтаксисом и типами данных. Форматы общего
назначения покрывают базовые типы: строки, числа, словари и списки. Они не
настолько гибки, но гарантируют поддержку в разных языках. Наоборот, форматы,
созданные для конкретной платформы, обеспечивают тесную интеграцию с ней, но не
популярны в других языках.

Стандарты некоторых форматов официально поддерживают систему тегов. Теги опасны
тем, что при их большом количестве конфигурация превращается в код. Но эта
возможность полезна в качестве запасного варианта.

Экосистема Clojure предлагает несколько интересных библиотек для конфигурации
приложения. Они различаются замыслом и архитектурой, и наверняка каждый
разработчик найдет то, что ему по душе.

Нет однозначного ответа на вопрос о том, какой формат, библиотека или рабочий
процесс лучше. В каждой фирме действуют свои соглашения и порядки. Разработчик
должен быть готов к тому, в новой команде конфигурацию готовят по-другому.
