\chapter{Системы}

\begin{teaser}
В этой главе мы поговорим о системах. Рассмотрим, как составить проект из
отдельных частей и заставить работать их вместе.
\end{teaser}

Понятие системы тесно связано с конфигурацией, которую мы только
обсудили. Конфигурация учит, как принять параметры из внешнего мира. Система
задает внутреннее устройство программы. Это набор компонентов со связями и
иерархией.

Система зависит от конфигурации, но не строго один к одному. Для одной и той же
конфигурации бывают разные системы и наоборот. Главное отличие в следующем:
конфигурация отвечает на вопрос как получить параметры, а система знает, как ими
распорядиться.

\section{Подробнее о системе}

Понятие системы появилось в момент, когда возник спрос на долгоиграющие
приложения. При разработке скриптов или утилит вопрос не стоял остро. Время
работы скрипта обычно коротко, и его состояние тоже живет недолго. При
завершении программы ресурсы освобождаются, поэтому нет смысла в особом контроле
за ними.

Все по-другому с серверными приложениями. Они работают постоянно и потому
устроены иначе, чем разовые скрипты. Приложение состоит из компонентов, которые
работают в фоне. Каждый компонент выполняет узкую задачу. При запуске приложение
включает компоненты в правильном порядке и строит между ними связи.

Компонент это объект, который несет состояние. На него действуют две операции:
включить и выключить. Как правило, включить компонент означает установить
соединение с каким-либо ресурсом, а выключить~--- закрыть его.

Типичные компоненты веб-приложения это HTTP-сервер, база данных, кэш. Чтобы не
открывать соединение на каждый запрос к базе, понадобится пул соединений. Но
создавать его вручную и передавать в функции это слишком рутинная задача. Должен
быть компонент, который при включении открывает пул и хранит его. Для сторонних
потребителей компонент предлагает функции для работы с базой. Внутри функции
опираются на открытый пул.

На первый взгляд схема напоминает ООП и принцип инкапсуляции. Не торопитесь с
выводами: в Clojure компоненты работают иначе. Ниже мы рассмотрим техническую
разницу между объектами и компонентами.

\section{Зависимости}

Главная проблема системы это зависимости компонентов. В примере выше все
компоненты~--- сервер, база и кэш~--- работают обособленно. Подключение к базе
нужно только для отдельных страниц, а недоступность кэша не ломает
приложение. Это базовые компоненты системы.

Компоненты высокого уровня опираются на базовые. Это может быть фоновый поток,
который читает базу и отправляет письма. Будет неправильно, если компонент
откроет свои подключения к базе и почте. Вместо этого он принимает уже
запущенные компоненты и работает с ними как с черным ящиком.

Система это комбинация компонентов с учетом зависимостей. Ключевая обязанность
системы~--- запустить и остановить компоненты в верном порядке. Например, если
компонент A зависит от B и C, то к моменту запуска A последние два должны быть
запущены. При завершении программы компоненты B и C нельзя выключить до тех пор,
пока работает~A.

Система строит граф зависимостей между компонентами. Нужно обойти граф так,
чтобы удовлетворить запросы каждого компонента.

Система не должна быть запущена частично. Это случается, когда один из
компонентов бросил исключение: включились сервер и очередь задач, но база
недоступна. Это досадная ситуация, потому что мы застряли в пограничном
состоянии. Если включить систему второй раз, получим конфликты портов или
компоненты-дубли. Позже мы узнаем, как с этим бороться.

Будет правильно, если дойдя до проблемного компонента, загрузчик системы не
бросит исключение, а поместит его в переменную. Затем пойдет в обратном порядке
и выключит все, что успел включить. И только потом бросит исключение, которое
поймал.

В систему должно легко добавить новый компонент. Для этого систему описывают
декларативно. В идеальном случае система это данные~--- комбинация словарей и
списков. Код загрузки пробегает по ним и включает компоненты. Расширить систему
означает добавить новый узел в коллекцию.

Когда система знает о зависимостях компонентов, можно запустить ее
подмножество. Например, нужно отладить фоновый обработчик почты. Это компонент,
который зависит от базы и SMTP-сервера. Веб-сервер и кэш в данном случае не
нужны, и запуск всей системы лишним. Продвинутые системы предлагают функцию с
семантикой <<запусти это компонент и все его зависимости>>.

\section{Преимущества}

На первый взгляд кажется, что система~--- лишнее усложнение. Это новая
библиотека, соглашения в команде, рефакторинг и ограничения. Но первичные
неудобства окупаются со временем.

Система приводит проект в порядок. С ростом кодовой базы становится важно, чтобы
разные части проекта были в едином стиле. Удобно, когда продукт состоит из
отдельных сервисов с общими библиотеками. Разработчика выручит повторное
использование кода, в том числе банальное копирование.

Системы полезны на всех стадиях производства, особенно тестировании. Для тестов
запускают измененную версию системы, где некоторые компоненты работают
по-другому. Например, для отправки смс в подкладывают компонент, который пишет
сообщения в файл.

Подобно конфигурации, у системы свой жизненный цикл. Понимая этот цикл, можно
внести в систему корректировки. Например, внедрить операторы \spverb|if| или
\spverb|cond->|, чтобы добавить или убрать компоненты по условию.

\section{Подготовка к обзору}

В главе об изменяемых данных мы упоминали о системах \page{systems-intro}. Мы
узнали, как с помощью \spverb|alter-var-root| менять глобальные переменные. Идея
в том, чтобы вынести компонент в модуль и снабдить его функциями \spverb|start!|
и \spverb|stop!|. Функции переключают состояние модуля. Запуск системы сводится
к вызову функций в верном порядке.

Это слабое, любительское решение. Такая система не знает о зависимостях между
компонентами. Она хрупкая, работает в ручном режиме, и каждое изменение требует
проверки.

Clojure предлагает несколько библиотек для систем. Мы рассмотрим Mount,
Component и Integrant. Библиотеки различаются идеологией в
построении систем. Они по-разному описывают компоненты и связи между ними. Так
мы рассмотрим проблему с разных сторон.

Библиотеки нарочно следуют в таком порядке. С Mount легче начать, он
будет удачным выбором для новичков. Component стал промышленным
стандартом. Мы уделим ему больше внимания и поэтому ставим в
середину. Integrant замыкает обзор. Его рассматривают как альтернативу
Component, с котором читатель должен быть знаком.

В примерах нам бы хотелось избежать банальности. Вместо наивных \spverb|foo| и
\spverb|bar| построим настоящую систему, похожую на то, с чем вы столкнетесь на
практике. Система состоит из веб-сервера, базы данных и фонового процесса,
который обновляет записи в базе. Мы специально добавили его, чтобы научиться
работать с зависимостями. Нарисуем топологию системы:

\chart{chart-sys-1}

Стрелки на схеме означают отношения между компонентами. Выражение $A \to B$
означает <<А зависит от В>>. В нашей системе все компоненты зависят от
конфигурации. Дополнительно фоновый обработчик зависит от базы данных. Над этой
системой мы и будем работать до конца главы.

\section{База и воркер}

Мы упоминали, что открывать соединение на каждый запрос неоптимально. В
настоящих проектах с базой работают через пул. Это сущность с состоянием,
поэтому его тоже <<включают и выключают>>.

Некоторые базы хранят данные в памяти, например SQLite или H2. Преимущество в
том, что их не нужно запускать в отдельном процессе. База подключается как
библиотека и хранит данные в памяти приложения.

База в памяти удобна для быстрого старта, но не отражает реалии производства~---
то, к чему мы стремимся в этой книге. Для in-memory-баз не используют пулы
соединений. В этом нет смысла, потому что данные лежат в памяти, а не в
сети. Поэтому будем работать с реляционной БД PostgreSQL с пулом HikariCP.

В системе работает фоновый процесс, воркер. Он дополняет записи в базе данными
из сети. Предположим, фирма ведет аналитику посещений. Каждый раз, когда кто-то
открывает страницу, приложение пишет в базу адрес и данные запроса: заголовок
User-Agent и IP-адрес клиента. Чтобы строить отчеты по странам и городам, нужно
узнать их через сторонние сервисы. Это дорогая операция, поэтому записи ставят
статус <<ждет обработки>> и выносят логику в фон.

\section{Подготовка Docker}

Возможно, у вас уже установлен PostgreSQL. Тогда останется создать новую базу и
таблицу в ней. Если нет, самое время попробовать Docker. Это программа для
запуска приложений из образов. Под образом понимают специальный файл, в котором
приложение со всем необходимым для запуска. Запущенный образ называют
контейнером.

У контейнеров несколько преимуществ. Приложение стартует в изолированной среде и
поэтому отделено от основной системы. Кроме безопасности, это решает проблему
\emph{чистоты}~--- контейнер не оставляет следов работы, если это не настроено
специально.

В сети работает публичный репозиторий, откуда Docker качает образы. В нем
публикуют программы разных версий и комплектации. Например, если нужен
PostgreSQL строго 9.3, просто скачайте образ. Установка этой версии в систему,
скорее всего, обернется конфликтом с уже работающей базой.

Некоторые образы можно настроить переменными среды или файлами. Образ PostgreSQL
устроен так, что при старте он загружает все \spverb|*.sql| файлы из папки
\spverb|/docker-entrypoint-initdb.d|. Если назначить этой папке локальный путь с
sql-миграциями, получим готовую базу без строчки кода.

В комплекте с Docker идет утилита \spverb|docker-compose|. Она запускает из
файла конфигурации. По умолчанию файл называется
\spverb|docker-compose.yaml|. Это YAML-документ, где указаны образы и параметры
их запуска. В примере ниже один контейнер \spverb|postgres|, которому заданы
порты, пути и переменные среды.

\begin{verbatim}
version: '2'
services:
  postgres:
    image: postgres
    volumes:
      - ./initdb.d:/docker-entrypoint-initdb.d
    ports:
      - 5432:5432
    environment:
      POSTGRES_DB: book
      POSTGRES_USER: book
      POSTGRES_PASSWORD: book
\end{verbatim}

В папке \spverb|initdb.d| лежат sql-файлы для старта базы. Достаточно файла
\spverb|01.init.sql| с одной таблицей:

\begin{verbatim}
drop table if exists requests;
create table requests (
    id            serial primary key,
    path          text not null,
    ip            inet not null,
    is_processed  boolean not null default false,
    zip           text,
    country       text,
    city          text,
    lat           float,
    lon           float
);
\end{verbatim}

Если запустить \spverb|docker-compose up|, поднимется сервер PostgreSQL на порту
5432 с базой \spverb|book|. Этого хватит для дальнейшей работы. Больше мы не
будем говорить о Docker, поскольку эта тема заслуживает отдельной книги. Всю
информацию вы найдете на официальном сайте
проекта\footurl{https://docker.com}.

\section{Mount}

Библиотека Mount\footurl{https://github.com/tolitius/mount} описывает
сущности с двумя состояниями: запуск и остановка. По команде сущность
<<включается>> и принимает значение, которое вернул код из фазы запуска. При
выключении сработает код из фазы остановки.

Mount идет на трюки, чтобы упростить работу программисту. Сущности
напоминают глобальные переменные, которые меняют значение по команде. Внешне
Mount прост, поэтому удобен для начинающих.

Макрос \spverb|defstate| задает новую сущность. Макрос похож на форму
\spverb|def|: он тоже создает переменную в текущем пространстве. Вместо значения
\spverb|defstate| принимает два выражения для старта и остановки.

Пока сущность не включена, переменая хранит особое значение
\spverb|DerefableState|. Когда ее включили, переменная станет тем, что вернуло
выражение старта. При выключении срабатывает код останова, и переменная
становится \spverb|NotStartedState|.

\subsection{Первая сущность}

Напишем с помощью Mount компонент веб-сервера. Поместим его в модуль
\spverb|src/book/systems/mount/server.clj|. \spverb|App| это примитивное
приложение, которое всегда вернет статус 200.

\begin{verbatim}
(ns book.systems.mount.server
  (:require
   [mount.core :as mount :refer [defstate]]
   [ring.adapter.jetty :refer [run-jetty]]))

(def app (constantly {:status 200 :body "Hello"}))

(defstate server
  :start (run-jetty app {:join? false :port 8080})
  :stop (.stop server))
\end{verbatim}

В макросе \spverb|defstate| фазы отделены ключами \spverb|:start| и
\spverb|:stop|. Пока что мы только объявили состояние, но ничего не
включили. Если выполнить \spverb|server|, увидим следующее:

\begin{verbatim}
#DerefableState[{:status :pending, :val nil}]
\end{verbatim}

Чтобы запустить компонент, выполните \spverb|(mount/start)|. Функция пробегает
по всем сущностями и включает их. Выражение \spverb|(run-jetty ...)| под ключом
\spverb|:start| вернет сервер, который работает в фоне. После запуска браузер
покажет приветствие по адресу \spverb|http://127.0.0.1:8080|. Переменная
\spverb|server| станет эксемпляром \spverb|Server| из пакета \spverb|jetty|:

\begin{verbatim}
(type server)
org.eclipse.jetty.server.Server
\end{verbatim}

Чтобы выключить систему, выполните \spverb|(mount/stop)|. Обратите внимание, что
в выражении \spverb|(.stop server)| сущность обращается к самой себе. После
остановки \spverb|server| станет особым значением, которое означает завершение.

\begin{verbatim}
(mount/stop)
(type server)
mount.core.NotStartedState
\end{verbatim}

Так строят систему. Сперва находят сущности, которые работают на протяжении всей
программы. В основном это сетевые подключения или фоновые задачи. Затем выносят
их в модули, где описывают логику запуска и остановки.

\subsection{Связь с конфигурацией}

Выше мы допустили ошибку: параметры сервера <<захардкожены>> в теле
компонента. Мы уже обсудили чем это плохо; параметры должны быть в
конфигурации. Поскольку мы строим систему, вынесем конфигурацию в компонент. При
запуске он читает файл или переменные среды, выводит типы, валидирует данные.

Напишем модуль \spverb|config.clj|. Для краткости не будем расписывать спеку
\spverb|::config|: оставим только проверку на словарь. Фаза \spverb|:start|
читает EDN-файл, проверяет спекой и возвращает данные. Замените \spverb|:start|
на вызов \spverb|Yummy|, \spverb|Aero| или свое решение.

\begin{verbatim}
(ns book.systems.mount.config
  (:require
   [mount.core :as mount :refer [defstate]]
   [clojure.spec.alpha :as s]
   [clojure.edn :as edn]))

(s/def ::config map?)

(defstate config
  :start
  (-> "system.config.edn"
      slurp
      edn/read-string
      (as-> config
          (s/conform ::config config))))
\end{verbatim}

Компоненту не нужна фаза \spverb|:stop|, потому что он не хранит открытые
ресурсы. Улучшим сервер так, чтобы он зависел от конфигурации. Для этого заменим
его параметры ссылкой на словарь \spverb|config|.

Пусть файл \spverb|system.config.edn| содержит словарь, где ключ это имя
компонента, а значение~--- его параметры. Поместим сервер под ключ
\spverb|:jetty|:

\begin{verbatim}
{:jetty {:join? false :port 8088}}
\end{verbatim}

Обновим модуль сервера. В список \spverb|:require| добавим импорт конфигурации:

\begin{verbatim}
(:require
  [book.systems.mount.config :refer [config]])
\end{verbatim}

\noindent
Перепишем компонент, чтобы параметры приходили из словаря:

\begin{verbatim}
(defstate
  server
  :start
  (let [{jetty-opt :jetty} config]
    (run-jetty app jetty-opt))
  :stop (.stop server))
\end{verbatim}

На этом этапе у нас уже система из двух компонентов, где один зависит от
другого. Убедитесь, что после вызова \spverb|(mount/start)| сервер работает как
ожидалось.

\subsection{База данных}

Подготовим компонент базы данных. Понадобяться две библиотеки:
\spverb|clojure.java.jdbc| и \spverb|hikari-cp|. Первая предлагает доступ к
реляционным базам данных. Это набор функций, которые работают одинаково для
разных баз. Например, выражения:

\begin{verbatim}
(jdbc/get-by-id db :users 42)
(jdbc/insert! db :users {:name "Ivan" :email "ivan@test.com"})
\end{verbatim}

\noindent
прочитают и запишут пользователя в PostgreSQL, MySQL или Oracle. Для каждого
бекенда строится SQL с учетом его особенностей.

JDBC-функции принимают первым параметром т.н. \emph{JDBC-спеку}. Обычно это
словарь параметров подключения: адрес и порт сервера, имя базы, пользователь и
пароль. На каждый запрос JDBC создает новый источник данных, открывает
соединение, обменивается данными и закрывает его.

В спеке может быть ключ \spverb|:datasource| с уже подготовленным
источником. Тогда JDBC игнорирует другие ключи и работает напрямую с
\spverb|:datasource|. Библиотека \spverb|hikari-cp| предлагает функцию, чтобы
построить источник с пулом соединений. Каждый раз, когда мы запрашиваем
соединение у источника, получаем одно из открытых ранее.

Пул это объект с жизненным циклом, поэтому вынесем его в компонент. Подготовим
модуль \spverb|db.clj|:

\begin{verbatim}
(ns book.systems.mount.db
  (:require
   [mount.core :as mount :refer [defstate]]
   [hikari-cp.core :as cp]
   [book.systems.mount.config :refer [config]]))

(defstate db
  :start
  (let [{pool-opt :pool} config
        store (cp/make-datasource pool-opt)]
    {:datasource store})
  :stop
  (-> db :datasource cp/close-datasource))
\end{verbatim}

На старте мы возвращаем JDBC-спеку~--- словарь с ключом \spverb|:datasource|,
внутри которого пул. При выключении функция \spverb|close-datasource| закрывает
его и все открытые соединения. Добавим в конфигурацию настройки пула:

\begin{verbatim}
{:pool {:minimum-idle       10
        :maximum-pool-size  10
        :adapter            "postgresql"
        :username           "book"
        :password           "book"
        :database-name      "book"
        :server-name        "127.0.0.1"
        :port-number        5432}}
\end{verbatim}

Для экономии обозначим только основные параметры. Это свойства подключения
(хост, порт, пользователь, пароль) и размерность пула. Дополнительно можно
настроить каждую стадию его работы. Полный список опций смотрите на странице
проекта\footurl{https://github.com/tomekw/hikari-cp}.

Запустите систему и выполните запрос:

\begin{verbatim}
(mount/start)
(require '[clojure.java.jdbc :as jdbc])
(jdbc/query db "select 42 as answer")
;; ({:answer 42})
\end{verbatim}

\subsection{Фоновая задача}

Все готово для последнего компонента системы. Это воркер, который работает в
отдельном потоке. Он выбирает из базы необработанные записи и дополняет их из
стороннего сервиса.

В таблице \spverb|requests| мы храним адрес страницы и IP-адрес клиента. Флаг
\spverb|is_processed| это признак того, была ли уже обработана запись. Поля
\spverb|city|, \spverb|country| и другие по умолчанию равны \spverb|null|.

Задача воркера состоит из шагов:

\begin{itemize}

\item
  раз в интервал запрашивать запись с флагом \spverb|NOT is_processed|;

\item
  сделать запрос к сервису, который вернет гео-данные по IP;

\item
  обновить запись в транзакции.

\end{itemize}

Выразим воркер в терминах Mount. Поскольку задача работает в отдельном
потоке, очевидно это тред или футура с бесконечным циклом. Но мы бы хотели
остановить воркер по запросу. Значит, цикл не бесконечный, а с
условием. Например, проверкой состояния на каждом шаге. Это состояние должно
быть доступно и воркеру, и тому, кто им управляет.

В Clojure это решается футурой и атомом. В атоме хранят логический флаг~---
признак продолжения цикла. На каждом шаге футура проверяет флаг, и если он
истина, то в очередной раз выполняет задачу. Чтобы завершить футуру, совершают
два действия. Первое~--- устанавливают флаг в ложь. Второе~--- ждут до тех пор,
пока футура не станет \emph{реализованной} (анг. realized). В терминах Clojure
это значит, что футура остановилась.

Подготовим модуль воркера. Понадобится конфигурация, компонент базы, логирование
и HTTP-клиент:

\begin{verbatim}
(ns book.systems.mount.worker
  (:require
   [mount.core :as mount :refer [defstate]]
   [clojure.java.jdbc :as jdbc]
   [clj-http.client :as client]
   [clojure.tools.logging :as log]
   [book.systems.mount.db :refer [db]]
   [book.systems.mount.config :refer [config]]))
\end{verbatim}

Воркер это словарь с полями \spverb|:flag| и \spverb|:task|, состояние и
футура. Фаза \spverb|:start| готовит этот словарь. Функции \spverb|make-task|
пока что не существует, но считаем, что она вернет футуру. В фазе \spverb|:stop|
флаг становится ложью, и мы ждем, пока футура не остановится.

\begin{verbatim}
(defstate worker
  :start
  (let [{task-opt :worker} config
        flag (atom true)
        task (make-task flag task-opt)]
    {:flag flag :task task})
  :stop
  (let [{:keys [flag task]} worker]
    (reset! flag false)
    (while (not (realized? task))
      (log/info "Waiting for the task to complete")
      (Thread/sleep 300))))
\end{verbatim}

Код компонента должен быть небольшим. Чтобы упростить его, технические шаги
выносят в функции. Можно описать футуру прямо внутри \spverb|defstate|, но это
займет лишних десять строк, и логику станет труднее понять.

Добавим в EDN-файл параметры воркера. Достаточно одного поля~--- сколько
миллисекунд ждать на каждом шаге цикла.

\begin{verbatim}
{:worker {:sleep 1000}}
\end{verbatim}

Опишем функцию \spverb|make-task|. Она принимает состояние и параметры из EDN.

\begin{verbatim}
(defn make-task
  [flag opt]
  (let [{:keys [sleep]} opt]
    (future
      (while @flag
        (try
          (task-fn)
          (catch Throwable e
            (log/error e))
          (finally
            (Thread/sleep sleep)))))))
\end{verbatim}

\spverb|(Task-fn)| это функция, которая выполняет бизнес-логику
приложения. Недостаточно просто вызвать функцию; нужно обернуть ее в цикл с
условием и перехватом ошибок, чтобы футура не завершилась аварийно. Если была
ошибка, пишем ее в лог и переходим на следующую итерацию.

Тем временем кто-то мог установить \spverb|flag| в ложь. Если это произошло,
выходим из цикла \spverb|while|, и футура завершается.

Теперь опишем \spverb|task-fn|. Функция читает из базы записи, которые ждут
обработки. Для каждой записи ищем гео-данные по IP с помощью
\spverb|get-ip-info|. Пока что мы не знаем, как работает поиск, но известно, что
это словарь с полями \spverb|:city|, \spverb|:country| и другими.

\begin{verbatim}
(defn task-fn []
  (jdbc/with-db-transaction [tx db]
    (when-let [request (first (jdbc/query tx query))]
      (let [{:keys [id ip]} request
            info   (get-ip-info ip)
            fields {:is_processed true
                    :zip (:postal_code info)
                    :country (:country_name info)
                    :city (:city info)
                    :lat (:lat info)
                    :lon (:lng info)}]
        (jdbc/update! tx :requests
                      fields
                      ["id = ?" id])))))
\end{verbatim}

Запрос на чтение мы вынесли в переменную \spverb|query|, чтобы сократить код
\spverb|task-fn|. Это SQL-выражение с оператором \spverb|FOR UPDATE|. Оператор
означает, что запись блокируется на изменение в других соединениях.

\begin{verbatim}
(def query
  "SELECT * FROM requests
   WHERE NOT is_processed
   LIMIT 1 FOR UPDATE;")
\end{verbatim}

\spverb|FOR UPDATE| работает только в транзакции, поэтому тело функции обернуто
в \spverb|(jdbc/with-db-transaction)|. Это макрос, внутри которого доступно
\emph{транзакционное} соединение с базой. На него указывает символ
\spverb|tx|. Всем JDBC-функциям мы передаем \spverb|tx|, а не \spverb|db|.

Осталось написать \spverb|get-ip-info|. Функция обращается стороннему
API. Подойдет любой сервис, который принимает \spverb|IP|-адрес и возвращает
сведения о нем в JSON. В промышленных системах это может быть API Google или
локальная база адресов.

\begin{verbatim}
(defn get-ip-info [ip]
  (:body (client/post "https://iplocation.com"
                      {:form-params {:ip ip}
                       :as :json})))
\end{verbatim}

Если вызвать \spverb|get-ip-info| с адресом Берлина, получим словарь:

\begin{verbatim}
(get-ip-info "85.214.132.117")

{:postal_code "12529"
 :continent_code "EU"
 :region_name "Land Berlin"
 :city "Berlin"
 :isp "Strato AG"
 :region "BE"
 :country_code "DE"
 :country_name "Germany"
 :time_zone "Europe/Berlin"
 :lat 52.5167
 :company "Strato AG"
 :lng 13.4}
\end{verbatim}

Мы описали последний элемент воркера, и он готов к работе. Добавим в базу
несколько записей, запустим воркер и через некоторое время прочтем их снова.

\begin{verbatim}
(jdbc/insert! db :requests {:path "/help" :ip "31.148.198.0"})
(mount/start)
;; wait for a while
(jdbc/query db "select * from requests")
({:path "/help" :ip "31.148.198.0" :is_processed true
  :city "Pinsk" :zip "225710" :id 1
  :lon 26.0728 :lat 52.1214 :country "Belarus"})
\end{verbatim}

Все верно, и мы переходим к последнему этапу.

\subsection{Все вместе}

Компоненты готовы и работают по отдельности; осталось собрать их в единое
целое. Напишем модуль, который импортирует компоненты. Вызов
\spverb|(mount/start)| из этого модуля запустит их все.

Мы упоминали, что функции \spverb|start| и \spverb|stop| работают только с теми
компонентами, которые известны Mount. Если загрузить модуль воркера, то
Mount узнает о компонентах \spverb|worker|, \spverb|db| и
\spverb|config|. Модуль сервера не будет загружен, и система не узнает про его
компонент. Общий модуль решает эту проблему.

\begin{verbatim}
(ns book.systems.mount.core
  (:require
   [mount.core :as mount]
   book.systems.mount.server
   book.systems.mount.worker))

(defn start []
  (mount/start))
\end{verbatim}

Мы не указали модули \spverb|db| и \spverb|config| в импорте. Это не
обязательно: \spverb|server| и \spverb|worker| зависят от них, и компилятор
загрузит их автоматически.

\subsection{Зависимости}

В начале главы мы говорили о главной проблеме систем. Это зависимости между
компонентами, их разрешение и порядок обхода. Рассмотрим, как Mount
решает эти задачи.

Читатель заметил, что при объявлении компонента мы не указываем его
зависимости. \spverb|Worker| нуждается в \spverb|config| и \spverb|db|, но об
этом нигде не сказано. Когда мы вызываем \spverb|(mount/start)|, система
угадывает порядок запуска: \spverb|config|, \spverb|db|, \spverb|worker|. Если
переставить любые два элемента, система не запустится. Как это работает?

Чтобы вычислить порядок, Mount полагается на компилятор
Clojure. Пространства имен зависят друг от друга, как компоненты в
системе. Компилятор ищет в теле \spverb|ns| ссылки на другие модули и загружает
их первыми. Вспомним, как выглядит \spverb|ns| воркера:

\begin{verbatim}
(ns book.systems.mount.worker
  (:require
   [book.systems.mount.db :refer [db]]
   [book.systems.mount.config :refer [config]]))
\end{verbatim}

Начертим граф зависимостей:

\chart{chart-sys-2}

Компилятор не загрузит \spverb|mount.worker| до тех пор, пока не разрешит
зависимости. Он начнет с модуля \spverb|mount.db|. Его упрощенное определение:

\begin{verbatim}
(ns book.systems.mount.db
  (:require
   [book.systems.mount.config :refer [config]]))
\end{verbatim}

Что с точки зрения компилятора:

\chart{chart-sys-3}

Прежде чем загрузить \spverb|db|, компилятор займется \spverb|config|. Он не
зависит других модулей будет и загружен первым. Затем компилятор вернется к
\spverb|db| и загрузит его. Дальше он поднимется на уровень
\spverb|worker|. Модуль \spverb|db| загружен, следующий по списку
\spverb|config|. Конфигурация уже загружена на этапе \spverb|db|. В Clojure
модуль не может быть загружен дважды, поэтому компилятор пропустит его. На
последнем шаге загрузится \spverb|worker|.

Мы вывели порядок загрузки \emph{пространств}: \spverb|config|, \spverb|db|,
\spverb|worker|. Каждая форма \spverb|defstate| выполняется в той же очереди. В
этом и заключается трюк: вызов \spverb|defstate| увеличивает внутренний счетчик
Mount. В момент создания компонент запоминает это число. Сущности
\spverb|config|, \spverb|db| и \spverb|worker| получат номера 1, 2 и 3. Чтобы
запустить систему, Mount сортирует компоненты по возрастанию номера, а для
остановки~--- по убыванию.

\subsection{Внутреннее устройство}

Mount хранит сведения о компонентах в приватных атомах. Они недоступны
сторонним модулям, но Clojure оставляет возможность добраться до них. Когда
компоненты загружены, выполните выражение:

\begin{verbatim}
(def _state @@(resolve 'mount.core/meta-state))
\end{verbatim}

В переменной \spverb|_state| окажется словарь компонентов. Двойной оператор
\spverb|@| играет следующую роль. Функция \spverb|resolve| по символу возвращает
объект \spverb|Var|. Из прошлых глав мы помним, что это контейнер, который
хранит значение. Первый \spverb|@| извлекает значение из \spverb|Var|; это атом
со словарем. Второй \spverb|@| извлекает словарь из атома.

Ключ словаря это \emph{текстовая} ссылка на компонент, например
\spverb|#'book.systems.mount.config/config|. Ей назначен другой словарь с
информацией о состоянии компонента. Нас интересует поле \spverb|:order|~--- тот
самый счетчик, по возрастанию которого нужно включить компоненты.

Расставим компоненты вручную. Видим, что порядок их загрузки верный:

\begin{verbatim}
(->> _state
     vals
     (sort-by :order)
     (map #(-> % :var meta :name)))
;; (config server db worker)
\end{verbatim}

Выражение ниже вернет словарь запущенных компонентов с похожей структурой:

\begin{verbatim}
@@(resolve 'mount.core/running)
\end{verbatim}

Атом \spverb|state-seq| хранит глобальный счетчик компонентов. Чтобы прочитать
его, выполните:

\begin{verbatim}
@@(resolve 'mount.core/state-seq)
\end{verbatim}

Получится 4, что верно: значения от 0 до 3 уже заняты другими компонентами.

При работе с Mount вы не должны менять его внутренние атомы. Примеры
выше нужны для того, чтобы читатель лучше понял устройство библиотеки.

\subsection{Состояние}

Легкость, с которой компонент изменяется при вызове \spverb|start| и
\spverb|stop|, похожа магию. Макрос \spverb|defstate| скрывает технические шаги,
которые срабатывают в момент его работы. На нижнем уровне состояние работает на
функции \spverb|alter-var-root|, которую мы рассмотрели в главе про
изменяемость \page{alter-var-root}.

Вспомним компонент сервера:

\begin{verbatim}
(defstate server
  :start (let [{jetty-opt :jetty} config]
           (run-jetty app jetty-opt))
  :stop (.stop ^Server server))
\end{verbatim}

В общих словах, \spverb|defstate| разворачивается в несколько определений. Это
глобальная переменная без значения

\begin{verbatim}
(def server)
\end{verbatim}

\noindent
и две анонимные функции старта и останова. Тела функций это формы
\spverb|:start| и \spverb|:stop|.

\begin{verbatim}
(fn start []
  (alter-var-root #'server
   (fn [_]
     (let [{jetty-opt :jetty} config]
       (run-jetty app jetty-opt)))))

(fn stop []
  (alter-var-root #'server
   (fn [_]
     (.stop ^Server server))))
\end{verbatim}

Mount помещает ссылки на эти функции в атом \spverb|meta-state|. Чтобы
включить компонент, нужно найти в словаре функцию включения и вызвать
ее. Функция назначит переменной \spverb|#'server| новое значение. Остановка
работает аналогично.

\subsection{Выборочный запуск}

До сих пор мы запускали систему целиком. Вызов \spverb|(mount/start)| без
параметров пробегает по \spverb|meta-state| и включает все компоненты. Это не
всегда удобно. Предположим, мы работаем над воркером и хотели бы запустить
только его и зависимости. В этом случае веб-сервер не нужен.

Чтобы запустить только нужные компоненты, их передают в функцию. Важно:
компонент должен быть не значением, а объектом \spverb|Var|.

\begin{verbatim}
(mount/start #'book.systems.mount.config/config
             #'book.systems.mount.db/db
             #'book.systems.mount.worker/worker)
\end{verbatim}

Если передать значение, Mount не запустит компонент. В примере ниже не
будет ошибки или сообщения, просто ничего не произойдет:

\begin{verbatim}
;; does nothing
(mount/start book.systems.mount.config/config
             book.systems.mount.db/db
             book.systems.mount.worker/worker)
\end{verbatim}

Тот факт, что функция ожидает именно \spverb|Var|, а не значение, сбивает с
толку новичков. Это не очевидно, поскольку в Clojure мы редко прибегаем к
переменным.

Минус ручного запуска в том, что он не контролирует зависимости. Mount
не хранит зависимости; библиотека знает только порядок компонентов, но не как
они связаны. Предположим, мы забыли, что базе и воркеру требуется конфигурация:

\begin{verbatim}
(mount/start #'book.systems.mount.db/db
             #'book.systems.mount.worker/worker)
\end{verbatim}

Выражение бросит странное исключение. Оно возникнет в компоненте \spverb|db|,
где создается пул. Объект \spverb|config| не запущен, и выражение
\spverb|(:pool config)| вернет \spverb|nil|. При попытке создать пул из \spverb|nil| получим
исключение.

С ростом системы становится сложнее отслеживать зависимости. Это слабое место
Mount~--- чтобы запустить подсистему, компоненты перечисляют
вручную. Чтобы облегчить этот сценарий, библиотека предлагает \emph{селекторы}
компонентов: \spverb|only|, \spverb|except| и другие.

\spverb|Except| вернет имена компонентов \emph{кроме} перечисленных. Если
передать результат в start, получим систему без указанных компонентов. Пример
ниже запустит подмножество без веб-сервера:

\begin{verbatim}
(-> [#'book.systems.mount.server/server]
    mount/except
    mount/start)
\end{verbatim}

Другие селекторы и их комбинации описаны на странице проекта в GitHub.

\subsection{Проблема перезагрузки}

В режиме разработки редактор подключен к сеансу REPL. Когда мы меняем код,
редактор отправляет изменения серверу. Возникает вопрос: что случится, если
внести правки в уже написанный компонент? Как Mount отреагирует на
перезагрузку модуля?

Если вы работаете в Emacs и Cider, подключитесь к проекту через
\spverb|M-x cider-connect|. Запустите систему, как мы делали это выше. Откройте модуль
сервера и выполните \spverb|M-x cider-eval-buffer| (или клавишами
\spverb|C-c C-k|). Команда выполнит файл на сервере. Все определения, включая \spverb|ns|,
\spverb|def|, и \spverb|defstate| сработают повторно.

Вы увидете логи с текстом, что сервер был перезагружен. Mount учитывает
этот сценарий. Макрос \spverb|defstate| проверяет, что такой компонент уже
объявлен и перезагружает его.

Перезагрузка это не всегда желаемое поведение. При частых изменениях может
случиться <<рассинхрон>>~--- ситуация, когда компонент считается выключенным, но
его ресурс занят. Например, в блоке \spverb|:stop| мы не вызвали метод
\spverb|(.stop)|. Если перезагрузить такой компонент, получим ошибку о том, что
порт занят.

Поведение компонента при перезагрузке задают в метаданных. Это поле
\spverb|:on-reload|, которое по умолчанию равно \spverb|:restart|. С ним
компонент перезагружает себя при повторном вызове \spverb|defstate|. Если задать
\spverb|:stop|, компонент остановится. Наиболее удобно значение \spverb|:noop|,
что значит не делать ничего. Компонент с метаданными выглядит так:

\begin{verbatim}
(defstate ^{:on-reload :noop} server
  :start (run-jetty app {:join? false :port 8080})
  :stop (.stop server))
\end{verbatim}

Старайтесь указывать \spverb|:on-reload| своим компонентам. \spverb|:Noop|
удобен тем, что освобождает от побочных эффектов. Возможно, вы не меняли
компонент, а только исправили опечатку в комментарии. Перезагружать компонент в
таком случае не требуется. Но даже если вы исправили компонент, перезагрузите
его вручную.

\subsection{Самостоятельная работа}

Вернемся к функции \spverb|get-ip-info| из модуля воркера. Для каждого IP-адреса
она выполняет HTTP-запрос. На низком уровне мы открываем TCP-соединение,
работаем с ним и закрываем. Это не оптимально, и проблему решают так же, как и с
базами данных~--- пулом соединений. Изучите пример из библиотеки
clj-http:\footurl{https://github.com/dakrone/clj-http}

\begin{verbatim}
;; create a new pool
(def cm (clj-http.conn-mgr/make-reusable-conn-manager
         {:timeout 2 :threads 3}))

;; make a request within the pool
(client/get "http://example.org/"
            {:connection-manager cm})

;; shut down the pool
(clj-http.conn-mgr/shutdown-manager cm)
\end{verbatim}

Напишите компонент, который шлет запросы через пул. Параметры компонента
(тайминг, число потоков) приходят из конфигурации. На старте компонент запускает
пул, при остановке закрывает его. Перепишите воркер так, чтобы он зависел от
нового компонента.

\section{Component}

Библиотека Component\footurl{https://github.com/stuartsierra/component}
тоже описывает компоненты и систему. Это небольшой фремворк, в котором главную
роль играет не объем кода, а идея. Дизайн Component в корне отличаются
от Mount, который мы рассмотрели выше.

Как и в Mount, на компонент действуют операции \spverb|start| и
\spverb|stop|. Каждая их них возвращает \emph{копию} объекта в новом
состоянии. Исходный компонент остается прежним. Можно сказать, компоненты
неизменяемы. Это отсекает целый пласт ошибок, связанных с состоянием.

Система это комбинация компонентов с зависимостями. Сначала система в состоянии
покоя; компоненты еще не были запущены. Специальный код обходит компоненты и
включает их. Получается запущенная копия системы. Она аналогична исходной, но
каждый ее компонент заменен на включенную версию себя. Аналогично работает
остановка: получится выключенная копия системы.

Идеи Component не терпят глобального состояния: сущности это обычные объекты. В
библиотеке нет скрытых атомов, которые хранят информацию о компонентах. Один
компонент имеет доступ к другому только если они зависимы. Компонент не должен
хранить состояние в атоме или другой изменяемой сущности. На каждое действие он
возвращает новую копию себя.

\subsection{Устройство}

Компонент это объект, который реализует протокол \spverb|Lifecycle|. Протокол
задает операции \spverb|start| и \spverb|stop|. Чаще всего компонент задают
типизированным словарем. Это сущность, которую объявляют формой
\spverb|defrecord|. По-другому их называют <<типизированные записи>> или
<<р\'{е}корды>>.

Запись отличается от словаря тем, что заранее перечисляет свои ключи. Они
называют \emph{слотами} записи. Доступ к слотам работает быстрее, чем у обычного
словаря. Компонент резервирует слоты для входных параметров и состояния.

Форма \spverb|defrecord| сочетаются на уровне языка. При объявлении записи можно
сразу расширить ее протоколом. В методах протокола слоты доступны как локальные
переменные. Это уменьшает код и экономит наше внимание.

Компонент таит в себе состояние, и только он знает, как им управлять. Будет
ошибкой делить компонент на части и передавать их функции. Внешним потребителям
компонент предлагает методы, которые заданы в отдельном протоколе.

Программирование на Component напоминает ООП. Компонент это объект с данными и
набором операций над ним. Как и классы, компонент инициируют, запускают и
останавливают.

Разница в том, что компоненты неизменяемы. Переход в новое состояние не меняет
слоты объекта, в то время как в промышленных языках их переписывают. Принцип
\spverb|SOLID| и классическая тройка <<инкапсуляция, наследование, полиморфизм>>
не имеют той же силы в Clojure. Б\'{о}льшая часть этих правил отпадает за
ненадобностью. Программируя на Clojure, мы не волнуемся о том, что нарушили
постулаты ООП.

\subsection{Первый компонент}

Перейдем к практике: перепишем систему из прошлого раздела на Component. Начнем
с веб-сервера. В файле \spverb|server.clj| объявим пространство имен:

\begin{verbatim}
(ns book.systems.comp.server
  (:require
   [com.stuartsierra.component :as component]
   [ring.adapter.jetty :refer [run-jetty]]))
\end{verbatim}

Компонент это запись с двумя слотами: \spverb|options| и \spverb|server|. В
опциях записаны параметры Jetty-сервера, в \spverb|server|~--- его
экземпляр. Строка \spverb|component/Lifecycle| означает протокол, который
реализует запись. Ниже следует \emph{реализация} протокола.

\begin{verbatim}
(defrecord Server [options server]

  component/Lifecycle

  (start [this]
    (let [server (run-jetty app options)]
      (assoc this :server server)))

  (stop [this]
    (.stop server)
    (assoc this :server nil)))
\end{verbatim}

Метод \spverb|start| вернет ту же запись, но с заполненным слотом
\spverb|:server|. В нем находится объект сервера. Метод \spverb|stop| принимает
запущенный компонент. Он выключает сервер и возвращает новый компонент, где слот
\spverb|:server| установлен в \spverb|nil|.

Обратите внимание: внутри методов мы свободно обращаемся к слотам, словно они
локальные переменные. Это работает только если методы расположены внутри
\spverb|defrecord|. Если расширить запись отдельной формой, например через
\spverb|extend|, доступ к слотам теряется. Придется извлекать слоты из
переменной \spverb|this|.

Сущность \spverb|Server| это не компонент, а только абстрактное описание. На
первом шаге его \emph{инициируют}, то есть создают экземпляр. Для этого служит
функция \spverb|map-><Record>|, где \spverb|<Record>| это имя записи. Макрос
\spverb|defrecord| автоматически порождает эту функцию. В нашем случае она
называется \spverb|map->Server|. Функция принимает обычный словарь и возвращает
его типизированную версию. Ключи словаря совпадают со слотами записи. Если ключ
не найден, слот равен \spverb|nil|.

\begin{verbatim}
(def s-created
  (map->Server
   {:options {:port 8080 :join? false}}))
\end{verbatim}

Переменная \spverb|s-created| это экземпляр записи \spverb|Server|. Мы указали
слот \spverb|options|, но не \spverb|server|. В этом нет смысла, потому что
\spverb|server| будет заполнен позже.

\begin{verbatim}
(def s-started (component/start s-created))
\end{verbatim}

Это выражение вернет \emph{запущенную} версию компонента. Откройте браузер по
адресу \spverb|http://127.0.0.1:8080| и проверьте, что сервер работает. У записи
\spverb|s-started| слот \spverb|:server| заполнен:

\begin{verbatim}
(-> s-started :server type)
org.eclipse.jetty.server.Server
\end{verbatim}

Остановите компонент. Проверьте, что страница больше не открывается, а слот
равен \spverb|nil|.

\begin{verbatim}
(def s-stopped (component/stop s-started))
(:server s-stopped) ;; nil
\end{verbatim}

Мы прошли полный цикл одного компонента: подготовку, запуск и остановку. Переход
на каждую стадию возвращает новый компонент. На практике вам не придется
управлять ими поштучно~--- этим займется система.

\subsection{Конструктор}

Вспомним, как мы создали экземпляр \spverb|Server|:

\begin{verbatim}
(map->Server {:options {:port 8080 :join? false}})
\end{verbatim}

У этой записи недостаток: мы должны помнить, какие слоты нужны для
инициализации, а какие для внутреннего состояния. Для простых записей это не
критично, но на практике бывают компоненты с десятью и более слотами. Чтобы не
запутаться, объявляют функцию-\emph{конструктор}.

Конструктор принимает только те аргументы, которые нужны для инициализации
компонента. В нашем случае это \spverb|options|, поэтому функция выглядит так:

\begin{verbatim}
(defn make-server
  [options]
  (map->Server {:options options}))

(def s-created (make-server {:port 8080 :join? false}))
\end{verbatim}

Конструктор упрощает создание компонента: с ним невозможно передать в
\spverb|map->Server| что-то лишнее. Конструктор это функция, поэтому к нему
можно добавить докуменатцию и спеку. Продвинутый редактор подскажет сигнатуру в
месте вызова. Добавляйте конструктор даже для тривиальных компонентов.

\subsection{Особенность слотов}

При остановке сервера мы совершаем два действия: вызываем у него метод
\spverb|(.stop)| и заменяем слот на \spverb|nil|. Почему бы не заменить
\spverb|assoc| на \spverb|dissoc|? Зачем хранить \spverb|nil|, когда можно
отсоединить поле?

\begin{verbatim}
(assoc this :server nil)
;; why not this?
(dissoc this :server)
\end{verbatim}

Причина в том, как устроены записи и слоты. Запись сохраняет уникальные свойства
до тех пор, пока ее слоты на месте. Если забрать у записи слот через
\spverb|dissoc|, получим обычный словарь. Покажем это на примере:

\begin{verbatim}
(-> s-stopped (assoc :server nil) type)
book.systems.comp.server.Server

(-> s-stopped (dissoc :server) type)
clojure.lang.PersistentArrayMap
\end{verbatim}

Если компонент вызывает \spverb|dissoc| на самом себе, на новой стадии мы
получим не компонент, а словарь. Это ошибка ведет к странному поведению
системы. Например, при попытке выключить компонент он продолжает работу.

Когда запись расширяют протоколом, тем самым строят связь между типом первого
аргумента и логикой. Для аргумента с типом \spverb|Server| методы \spverb|start|
и \spverb|stop| выполнят одно, для \spverb|DB| или \spverb|Worker|~---
другое. По умолчанию \spverb|start| и \spverb|stop| просто вернут объект.


%%  Это
%% значит, что если метод start вернул не компонент, а словарь, мы не сможем
%% вызвать правильный stop для этого словаря.

Приведем неудачный пример. Его метод \spverb|start| возвращает словарь с полем
\spverb|server|:

\begin{verbatim}
(defrecord BadServer [options server]
  component/Lifecycle
  (start [this]
    {:server (run-jetty app options)})
  (stop [this]
    (.stop server)
    nil))
\end{verbatim}

Сервер запустится без ошибок. Но переменная \spverb|bs-started| уже не запись, а
словарь. Метод \spverb|stop| словаря вернет его же без каких либо
действий. Можно сколько угодно вызывать \spverb|component/stop|, но сервер не
остановится.

\begin{verbatim}
(def bs-created (map->BadServer {:options {:port 8080 :join? false}}))
(def bs-started (component/start bs-created))
(type bs-started)
;; clojure.lang.PersistentArrayMap
(component/stop bs-started)
;; does nothing, the server still works
\end{verbatim}

Похожие трудности возникнут, если исправить \spverb|start| без учета
\spverb|stop|. При остановке компонент выключит сервер, но вернет
\spverb|nil|. При повторном запуске \spverb|nil| получим исключение, что тип не
реализует протокол \spverb|Lifecycle|.

Следите за тем, чтобы компонент менял только значения слотов, но не их
состав. Код \spverb|start| и \spverb|stop| всегда заканчивается формой
\spverb|assoc| или \spverb|dissoc|.

\subsection{Компонент базы}

Напишем компонент для работы с базой. Концепция пула соединений уже известна из
прошлых разделов. Компонент содержит два слота: \spverb|options| и
\spverb|db-spec|. Первый это словарь опций будущего пула. Слот \spverb|db-spec|
хранит JDBC-спеку с открытым пулом.

\begin{verbatim}
(defrecord DB
    [options db-spec]

  component/Lifecycle

  (start [this]
    (let [pool (cp/make-datasource options)]
      (assoc this :db-spec {:datasource pool})))

  (stop [this]
    (-> db-spec :datasource cp/close-datasource)
    (assoc this :db-spec nil)))
\end{verbatim}

Добавим конструктор:

\begin{verbatim}
(defn make-db [options]
  (map->DB {:options options}))
\end{verbatim}

Компонент готов к запуску, и его можно прогнать через функции
\spverb|make-db|~$\to$ \spverb|component/start|~$\to$ \spverb|component/stop|.

Пока что неясно, как выполнить запрос через компонент. Нас интересует слот
\spverb|db-spec|, который хранит спеку. Технически можно вычленить его и
передать в jdbc-функцию:

\begin{verbatim}
(let [{:keys [db-spec]} db-started
      users (jdbc/query db-spec "select * from users")]
  (process-users users))
\end{verbatim}

Это варварский подход: нельзя вторгаться в компонент, даже если язык предлагает
такую возможность. Мы нарушаем идею, согласно которой компонент неделим для
потребителя. В этом плане компонент похож объект в современных языках.

Дополним \spverb|DB| методами для работы с базой. Поместим их в отдельный
протокол. Сигнатуры похожи на jdbc-функции с той разницей, что первый параметр
это не спека, а \spverb|this|, компонент:

\begin{verbatim}
(defprotocol IDB
  (query [this sql-params])
  (update! [this table set-map where-clause]))
\end{verbatim}

В теле \spverb|defrecord|, сразу после \spverb|stop|, реализуем новый
протокол. Все сводится к JDBC-функциям, в которые передаем слот
\spverb|:db-spec| и аргументы метода.

\begin{verbatim}
(defrecord BadServer [options server]
  ;; component/Lifecycle implementation

  IDB
  (query [this sql-params]
    (jdbc/query db-spec sql-params))

  (update! [this table set-map where-clause]
    (jdbc/update! db-spec table set-map where-clause)))
\end{verbatim}

Компонент готов к запросам. Мы вызываем не JDBC-функции, а методы протокола. Тем
самым мы изолируем зависимость компонента от JDBC.

\begin{verbatim}
(def db-created (make-db options))
(def db-started (component/start db-created))
(query db-started "select * from requests")
(update! db-started :requests {:is_processed false} ["id = ?" 42])
(def db-stopped (component/stop db-started))
\end{verbatim}

\subsection{Транзакционный компонент}

Для согласованных изменений в базе нужны транзакции. Раньше мы пользовались
\spverb|jdbc/with-db-transaction|. Из обычного соединения он получает
транзакционное и связывет его с символом.

В отличии от JDBC-версии, наш макрос работает с компонентом. Он принимает
обычный компонент и связывает с символом его транзакционную версию. Макрос
сводится к следующим шагам:

\begin{itemize}

\item
  получить спеку из компонента;

\item
  обернуть тело в макрос JDBC, получить транзакционное соединение;

\item
  связать с символом \spverb|comp-tx| компонент, у которого слот \spverb|:db-spec| заменен на
  транзакционное соединение.

\end{itemize}

\begin{verbatim}
(defmacro with-db-transaction
  [[comp-tx comp-db & trx-opt] & body]
  `(let [{db-spec# :db-spec} ~comp-db]
     (jdbc/with-db-transaction
       [t-conn# db-spec# ~@trx-opt]
       (let [~comp-tx (assoc ~comp-db :db-spec t-conn#)]
         ~@body))))
\end{verbatim}

Читатель заметит, что в макросе мы нарушили принцип закрытости компонента. Мы
вручную читаем и заменяем его приватный слот. Подумайте, как улучшить этот
код. Подсказка: чтение спеки можно вынести в метод \spverb|get-spec|. Добавьте
второй конструктор, который вернет компонент по спеке. Замените \spverb|assoc|
на вызов нового конструктора с \spverb|t-conn#|.

Макрос в действии:

\begin{verbatim}
(with-db-transaction
  [db-tx db-started]
  (let [query "select * from requests limit 1 for update"
        [request] (query db-tx query)
        {:keys [id]} request]
    (when request
      (update! db-tx :requests
               {:is_processed false}
               ["id = ?" id]))))
\end{verbatim}

В логах PostgreSQL увидим записи:

\begin{verbatim}
BEGIN
select * from requests limit 1 for update
UPDATE requests SET is_processed = $1 WHERE id = $2
DETAIL:  parameters: $1 = 'f', $2 = '3'
COMMIT
\end{verbatim}

Запросы \spverb|select| и \spverb|update| действительно сработали в транзакции.

\subsection{Воркер}

Напишем компонент воркера. Объявим модуль и подключим зависимости:

\begin{verbatim}
(ns book.systems.comp.worker
  (:require
   [com.stuartsierra.component :as component]
   [book.systems.comp.db :as db]
   [clj-http.client :as client]
   [clojure.tools.logging :as log]))
\end{verbatim}

Воркер это запись с двумя протоколами: \spverb|Lifecycle| и
\spverb|IWorker|. Протокол \spverb|Lifecycle| уже знаком читателю: это функции
\spverb|start| и \spverb|stop|. В \spverb|IWorker| поместим бизнес-логику. Это
задача и ее подготовка. Ожидаем, что \spverb|task-fn| это функция, которую
воркер вызывает на каждом шаге цикла. Метод \spverb|make-task| оборачивает ее в
цикл и \spverb|try/catch|.

\begin{verbatim}
(defprotocol IWorker
  (make-task [this])
  (task-fn [this]))
\end{verbatim}

Запись хранит четыре слота: входные опции, флаг продолжения, футура с циклом и
база данных. Последний слот~--- зависимость компонента. Это экземпляр
\spverb|DB|, который мы только что рассмотрели. Реализуем \spverb|Lifecycle|:

\begin{verbatim}
(defrecord Worker
  [options flag task db]
  component/Lifecycle
  (start [this]
    (let [flag (atom true)
          this (assoc this :flag flag) ;; <= note this
          task (make-task this)]
      (assoc this :task task)))

  (stop [this]
    (reset! flag false)
    (while (not (realized? task))
      (log/info "Waiting for the task to complete")
      (Thread/sleep 300))
    (assoc this :flag nil :task nil)))
\end{verbatim}

Обратите внимание на строку с меткой. Мы дополняем \spverb|this| флагом
состояния. Метод \spverb|make-task| ожидает \spverb|this| с заполненным слотом
\spverb|flag|. Если строку убрать, \spverb|make-task| получит запись с пустым
слотом.

Опишем протокол \spverb|IWorker|. Код \spverb|make-task| и \spverb|task-fn| уже
знаком читателю из раздела про Mount. Разница в том, что теперь мы
работаем не с функциями, а методами. Метод имеет прямой доступ к слотам, поэтому
нет смысла передавать параметры. Ниже мы перенесли код \spverb|make-task| и
\spverb|task-fn| в компонент. Для краткости опустим формы \spverb|try/catch| и
поиск геоданных.

\begin{verbatim}
(defrecord Worker
  ;; component/Lifecycle skipped
  IWorker

  (make-task [this]
    (future
      (while @flag ;; try/catch skipped
        (task-fn this))))

  (task-fn [this]
    (db/with-db-transaction [tx db]
      (when-let [[request] (db/query tx query)]
        (let [fields ...] ;; fields
          (db/update! tx :requests
                      fields ["id = ?" id]))))))
\end{verbatim}

Добавим конструктор, и компонент готов:

\begin{verbatim}
(defn make-worker
  [options]
  (map->Worker {:options options}))
\end{verbatim}

\subsection{Ручные зависимости}

Воркер отличается тем, что имеет зависимости. Пока что не ясно, как воркер
узнает о базе данных, потому что конструктор принимает только опции. Эту
проблему решает система, но не разработчик. Мы не должны передавать компоненты
друг другу при их создании.

Во время разработки это правило можно нарушить. Соберем мини-систему из двух
компонентов. Так мы проверим код, который написали и поймем, как работает
настоящая система. Проведем эксперимент в отдельном модуле
\spverb|core|. Добавьте в него конструкторы и библиотеку Component:

\begin{verbatim}
(ns book.systems.comp.core
  (:require
   [com.stuartsierra.component :as component]
   [book.systems.comp.worker :refer [make-worker]]
   [book.systems.comp.db :refer [make-db]]))
\end{verbatim}

Наивная система ниже. Это функция, которая принимает конфигурацию. Мы вручную
запускаем базу и воркер и возвращаем словарь компонентов. В строке с отметкой мы
задаем воркеру слот с компонентом базы. Важен момент, когда это происходит:
компонент базы \emph{уже включен}, а воркер еще нет (\spverb|assoc| перед
\spverb|start|).

\begin{verbatim}
(defn my-system-start
  [config]
  (let [{db-opt :pool
         worker-opt :worker} config
        db (-> db-opt
               make-db
               component/start)
        worker (-> worker-opt
                   make-worker
                   (assoc :db db) ;; !!!
                   component/start)]
    {:db db :worker worker}))
\end{verbatim}

Чтобы запустить систему, передайте в функциюв параметры пула и
воркера. Сохраните систему в переменной, чтобы выключить ее позже.

\begin{verbatim}
(def _sys (my-system-start {:pool {...} :worker {...}}))
\end{verbatim}

Пока система работает, проверьте ее, как мы уже это делали. Добавьте записи в
таблицу \spverb|requests| и убедитесь, что воркер дополняет их поля. Функция
выключения остановит компоненты в верном порядке.

\begin{verbatim}
(defn my-system-stop
  [system]
  (-> system
      (update :worker component/stop)
      (update :db component/stop)))

(my-system-stop _sys)
\end{verbatim}

\subsection{Промышленная система}

Рассмотрим промышленную систему. Функция \spverb|system-map| принимает цепочку
значений. Нечетные элементы это ключи компонентов, а четные~--- вызовы их
конструкторов. Функция возвращает систему в состоянии покоя.

Построение системы не должно носить побочных эффектов. Вызов \spverb|system-map|
строит дерево компонентов с заполненными слотами. Конструкторы только возвращают
записи. Если конструктор обращается к выводу, диску и состоянию, это грубая
ошибка.

Система зависит от конфигурации, поэтому ее оборачивают в функцию
\spverb|make-system|. Она принимает словарь конфигурации и делит его на
составные части. Каждый конструктор вызывают со своей частью. Удобно, когда
конфигурация повторяет систему: на верхнем уровне ключи компонентов, а под ними
словари опций.

Чтобы сообщить компоненту зависимости, его оборачивают в функцию
\spverb|component/using|. Второй аргумент это ключи компонентов, которые целевой
компонент должен получить до старта. Ключи могут быть вектором или
словарем. Если слот совпадает с именем компонента, это вектор. Если отличается,
передают словарь \spverb|{:slot-name :system-name}|.

Ниже функция \spverb|make-system| строит систему, о которой мы договорились в
начале главы. Компонент \spverb|worker| обернут в
\spverb|component/using|. Поскольку слот \spverb|:db| совпадает с именем
компонента в системе, мы передали вектор \spverb|[:db]|.

\begin{verbatim}
(defn make-system
  [config]
  (let [{:keys [jetty pool worker]} config]
    (component/system-map
     :server (make-server jetty)
     :db     (make-db pool)
     :worker (component/using
              (make-worker worker) [:db]))))
\end{verbatim}

Если компонент назывался \spverb|:storage|, мы бы задали словарь имен:

\begin{verbatim}
(component/system-map
 :server  (make-server jetty)
 :storage (make-db pool)
 :worker  (component/using
           (make-worker worker) {:db :storage}))
\end{verbatim}

Словарь полезен, когда подключают сторонние компоненты. Их создатели не знают,
как заданы сущности в вашем проекте. Например, чужой компонент зависит от
\spverb|:database|, а у нас это просто \spverb|:db|. Маппинг снимает проблему
расхождения имен.

Чтобы запустить систему, ее передают в \spverb|component/start|. У системы своя
реализация \spverb|Lifecycle|. При запуске она обходит компоненты и строит граф
зависимостей. Из графа система выводит порядок обхода. Перед тем как запустить
компонент с зависимостями, система сообщает их компоненту через \spverb|assoc|,
как мы делали это вручную. Аналогично работает выключение системы.

\begin{verbatim}
(def config {...})
(def sys-init (make-system config))
(def sys-started (component/start sys-init))
(def sys-stopped (component/stop sys-started))
\end{verbatim}

\subsection{Хранение системы}

Мы задали систему через \spverb|def|, что не совсем правильно. Система это
сущность, которую включают по требованию. Если \spverb|def| срабатывает в
функции, это грубая ошибка. С системой обращаются как с глобальной переменной,
которая меняет значение. Для этого подходит \spverb|alter-var-root|.

В модуле приложения объявляют переменную системы. Это делают макросом
\spverb|defonce|, чтобы случайно не затереть ее при перезагрузке модуля. Для
удобства можно задать \spverb|partial| от \spverb|alter-var-root| и переменной,
чтобы сократить код.

Компонент может быть в трех состояниях: покой, запуск и остановка. То же самое
относится к системе. Функции \spverb|system-init|, \spverb|system-start| и
\spverb|system-stop| переводят систему в нужное состояние. Первая функция
принимает словарь конфигурации.

\begin{verbatim}
(defonce system nil)

(def alter-system (partial alter-var-root #'system))

(defn system-init [config]
  (alter-system (constantly (make-system config))))

(defn system-start []
  (alter-system component/start))

(defn system-stop []
  (alter-system component/stop))
\end{verbatim}

Код выше дает все рычаги, чтобы управлять приложением. Функция \spverb|-main|
сводится к трем шагам: чтению конфигурации, подготовке системы и запуску.

\begin{verbatim}
(defn -main [& args]
  (let [config (load-config "config.edn")]
    (system-init config)
    (system-start)))
\end{verbatim}

Хотя система и глобальна, к ней нельзя обращаться напрямую. Если один компонент
извлекает другой из недр системы, это провал разработчика. Такой подход сводит
на нет саму идею системы и компонентов. Обращаться к системе можно только при
разработке или в тестах. Для б\'{о}льшей надежности систему делают приватной:
так мы обезопасим ее от обращения извне.

\begin{verbatim}
(defonce ^:private system nil)
\end{verbatim}

До приватной системы можно добраться с помощью \spverb|resolve| по ее полному
символу.

\subsection{Корректное завершение}

Глобальная система идет вразрез с тем, что пишет автор библиотеки. На странице
проекта встречается фраза: <<In production, the system map is ephemeral. It is
used to start all the components running, then it is discarded. (В промышленном
запуске система эфемерна. Она запускает все компоненты и затем исчезает.)>>

Это редкий случай, когда мы не согласимся с автором. Даже в боевом режиме вам
потребуется ссылка на систему. Без ссылки не получится корректно остановить
приложение (анг. \emph{graceful shutdown}). Под корректностью понимают то, что
все ресурсы закрыты.

Некоторые компоненты сложны: это очереди задач, каналы данных, транзакции. При
завершении нельзя полагаться на стандартные средства JVM. Если аварийно
завершить компонент очереди, мы потеряем сообщение или обработаем его
дважды. Закрывайте ресурсы правильно, даже если приходится ждать.

В боевом режиме приложение перехватывает \spverb|POSIX|-сигналы и реагирует на
них должным образом. Например, если поступил \spverb|SIGTERM|, приложение
останавливает систему, дожидается остановки и \emph{только потом} завершается.

Библиотека \spverb|spootnik/signal|\footurl{https://github.com/pyr/signal}
предлагает макрос, чтобы связать сигнал с реакцией на него. Подключите
библиотеку в проект:

\begin{verbatim}
;; project.clj
[spootnik/signal "0.2.2"]

;; src/book/systems/comp/core.clj
(ns ...
  (:require [signal.handler :refer [with-handler]]))
\end{verbatim}

Расширьте функцию \spverb|-main|. После запуска системы добавьте реакцию на
сигналы \spverb|SIGTERM|, \spverb|SIGINT| и \spverb|SIGHUP|. Первые два сигнала
останавливают систему и завершают программу. Сигнал \spverb|SIGHUP| мы
расцениваем как перезагрузку системы.

\begin{verbatim}
(with-handler :term
  (log/info "caught SIGTERM, quitting")
  (system-stop)
  (log/info "all components shut down")
  (System/exit)

(with-handler :int
  (log/info "caught SIGINT, quitting")
  (system-stop)
  (log/info "all components shut down")
  (System/exit))

(with-handler :hup
  (log/info "caught SIGHUP, reloading")
  (system-stop)
  (system-start)
  (log/info "system reloaded"))
\end{verbatim}

Заметим, что сигналы не работают, когда проект запущен через \spverb|lein run|
или в REPL. Чтобы проверить сигналы, соберите \spverb|uberjar| и запустите его
как Java-приложение.

\begin{verbatim}
lein uberjar
java -jar target/book-standalone.jar
\end{verbatim}

Нажмите \spverb|Ctrl-C|. Приложение завершится не сразу, и вы увидите логи о
том, что система остановлена.

Программы по управлению процессами обычно ждут 30 секунд, пока процесс не
завершится. В противном случае его завершают принудительно. Ожидание системы
должно быть разумным. Если на обработку сообщения уходит несколько минут,
фиксируйте промежуточный результат, чтобы возобновить его позже.

\subsection{Подробнее об ожидании}

Вспомним функцию \spverb|-main| приложения. Это входная точка программы на
Clojure:

\begin{verbatim}
(defn -main [& args]
  (let [config (load-config "config.edn")]
    (system-init config)
    (system-start)))
\end{verbatim}

У читателя, не знакомого с тонкостями JVM, возникнет вопрос. Почему программа не
завершается после \spverb|(system-start)|? После нее нет цикла, хука или
события. Почему платформа продолжает работать?

Это стандартное поведение JVM. Если программа завершается не аварийно, главный
поток ожидает, пока не остановятся дочерние. Запуск системы порождает новые
потоки (сервер, пул соединений); после \spverb|(system-start)| основной поток
повиснет в ожидании их завершения. Он будет ждать до тех пор, пока систему не
выключат в другом потоке или не придет \spverb|POSIX|-сигнал, на который задали
реакцию.

Если у компонента нет состояния, не будет и новых потоков. Компонент \spverb|DB|
можно изменить так, что слот \spverb|db-spec| это не пул соединений, а словарь
подключения. Бывают компоненты, которые просто выполняют разовую задачу на
старте. Если ни один компонент не порождает поток, программа выполнит
\spverb|start| для каждого из них и завершится.

\subsection{Улучшаем зависимости}

Вспомним, как мы сообщили компоненту \spverb|:worker| о его зависимостях:

\begin{verbatim}
(component/system-map
 ;; ...
 :worker (component/using
          (make-worker worker) [:db]))
\end{verbatim}

Когда компонентов много, зависимости вносят шум. Поможет трюк: поместим
зависимости в конструктор.

Выше \spverb|(make-worker {...})| возвращает наивный компонент, который не знает
о зависимостях. Он попадает в функцию \spverb|using|, которая сообщает
их. Поместим \spverb|component/using| в конструктор, чтобы новый компонент был
сразу <<заряжен>> зависимостями. Тогда на уровне системы \spverb|using| станет
не нужен.

Перепишем конструктор воркера:

\begin{verbatim}
(defn make-worker [config]
  (-> config
      map->Worker
      (component/using [:db])))
\end{verbatim}

Теперь система выглядит чище:

\begin{verbatim}
(component/system-map
 :server (make-server jetty)
 :db     (make-db pool)
 :worker (make-worker worker))
\end{verbatim}

Подход требует, чтобы имена в системе совпадали со слотами. Если это ваши
компоненты, договоритесь с командой об именах. Для сторонних компонентов легко
написать свой конструктор.

Рассмотрим, как устроены данные о зависимостях. Очевидно, вызов
\spverb|component/using| что-то сообщает компоненту, но компонент от этого не
меняется. Не возникает нового поля \spverb|:deps| или чего-то другого. Компонент
хранит зависимости в \emph{метаданных}.

Метаданные это словарь дополнительной информации об объекте. Метаданные работают
с коллекциями и некоторым другим типам Clojure, например, символом или
переменной. К метаданным прибегают, когда нужно сообщить объекту данные, не
изменяя его. Зависимости компонента подходят на эту роль.

Функция \spverb|meta| возвращает метаданные объекта. Пример ниже доказывает, что
конструктор вернул компонент с зависимостями:

\begin{verbatim}
(-> {...} make-worker meta)
#:com.stuartsierra.component{:dependencies {:db :db}}
\end{verbatim}

Чтобы увидеть метаданные другим способом, установите переменную
\spverb|*print-meta*| в истину. Тогда при печати объекта в REPL он будет
дополнен метаданными:

\begin{verbatim}
(set! *print-meta* true)
(make-worker {...})
^#:com.stuartsierra.component{:dependencies {:db :db}}
#book.systems.comp.worker.Worker{...}
\end{verbatim}

\subsection{Группировка слотов}

Слоты компонента делятся на три группы. Это поля инициализации, состояния и
зависимости. Вспомним компонент воркера:

\begin{verbatim}
(defrecord Worker
    [options flag task db])
\end{verbatim}

В этом примере слот \spverb|options| относится к инициализации, \spverb|flag| и
\spverb|task| к состоянию, \spverb|db|~--- зависимость. Чем сложнее компонент,
тем больше слотов в каждой группе. Когда слоты случайном порядке, трудно понять
их семантику. Считается хорошим тоном отделять слоты комментарием:

\begin{verbatim}
(defrecord Worker
    [;; init
     options
     ;; runtime
     flag
     task
     ;; deps
     db])
\end{verbatim}

Первой идет группа \spverb|init|, входные параметры. Это слоты, необходимые
новому компоненту. Ожидается, что конструктор принимает такие же
параметры. Группа \spverb|runtime| перечисляет слоты, которые компонент заполнит
при старте. В \spverb|deps| указаны зависимости. Они совпадают с вектором
ключей, который передают в \spverb|using| в конструкторе.

Группировка слотов облегчает работу с кодом. Договоритесь с командой, чтобы
внедрить эту практику. Когда слотов слишком много, это говорит о том, что
компонент переусложнен. Часть логики выносят в дочерний компонент и подключают в
зависимости первого.

На страницах книги мы не группируем слоты, потому что иначе код займет слишком
много места. В вашем проекте делайте это обязательно.

\subsection{Условная система}

В главе про конфигурацию мы писали про \emph{feature flags} \page{feature-flags}.
Это параметры, которые включают целые пласты логики. Флаги удобны тем,
что отпадает нужда в новой сборке приложения. Достаточно поменять
конфигурацию и перезагрузить сервис.

Иногда систему строят не линейно, а по условиям. Задача сводится к тому, чтобы
передать ключи и компоненты в функцию \spverb|system-map|. Если список
предварительно обработать аргументы, получим нужную функциональность.

Предположим, компонент воркера все еще в испытательном режиме. Добавим в
конфигурацию поле с семантикой <<включить воркер>>. Если оно ложь, система
запустится без этого компонента.

Выделим в конфигурации группу \spverb|:features|. Это флаги <<фич>>, которые под
вопросом:

\begin{verbatim}
{:features {:worker true}
 :jetty {:join? false :port 8088}
 ;; etc
}
\end{verbatim}

Перепишем функцию \spverb|make-system|. Прежде тем как попасть в
\spverb|system-map|, компоненты проходят отбор. Макрос \spverb|cond->|
<<пробрасывает>> вектор компонентов через цепочку условий и форм. Если условие
\spverb|worker?| вернет истину, следующая форма добавит к вектору значения
\spverb|:worker| и \spverb|(make-worker {...})|. Ниже могут быть другие флаги
или проверки.

\begin{verbatim}
(defn make-system [config]
  (let [{:keys [features jetty pool worker]} config
        {worker? :worker} features
        comps-base [:server (make-server jetty)
                    :db (make-db pool)]
        comps (cond-> comps-base
                worker?
                (conj :worker (make-worker worker)))]
    (apply component/system-map comps)))
\end{verbatim}

Убедимся, что флаг работает. Система это запись, поэтому функция \spverb|keys|
вернет ее ключи. Видно, что слот \spverb|:worker| появляется в зависимости от
флага:

\begin{verbatim}
(keys (make-system {:features {:worker false}}))
(:server :db)

(keys (make-system {:features {:worker true}}))
(:server :db :worker)
\end{verbatim}

Флаги облегчают работу с проектом. Некоторые компоненты сложны и требуют
специального окружения. Если можно задать их флагом, вы принесете пользу всей
команде.

\subsection{Спуск системы}

Компоненты свободно общаются друг с другом. Если одному компоненту понадобился
другой, мы ставим зависимость и добавляем слот. Проблемы начинаются, когда к
системе обращается не компонент, а другая сущность.

Чаще всего это обработчик HTTP-запроса, о которых мы говорили в первой
главе \page{first-handler}. Функция плохо ложится на идеи компонента: последний
хранит состояние, а функция, напротив, избегает его. Запуск и остановка функции
это бессмысленная операция. Функция и компонент противоположны друг другу.

Рассмотрим случай, когда обработчику запроса нужен компонент базы данных. Как
спустить часть системы в функцию, не нарушив принципы библиотеки? Обращение к
глобальной переменной мы не рассматриваем, потому что это слабое
решение. Проблему решают двумя способами: \emph{пробросом} и \emph{замыканием}.

Проброс означает, что отдельные компоненты передают в объекте запроса. Вариант
имеет право на жизнь, потому что запрос это часть сервера, а сервер это
компонент. Поэтому сервер имеет право добавить поля запросу.

Чтобы компонент базы стал доступен серверу, подключим его в зависимости. Изменим
слоты сервера и конструктор:

\begin{verbatim}
(defrecord Server
  [options server db])

(defn make-server
  [options]
  (-> (map->Server {:options options})
      (component/using [:db])))
\end{verbatim}

Расширим метод сервера \spverb|start|. Если раньше мы передавали \spverb|app|
напрямую в \spverb|run-jetty|, то теперь вводим дополнительный шаг. Функция
\spverb|make-handler| оборачивает \spverb|app| таким образом, что каждый запрос
в \spverb|app| дополнен базой.

\begin{verbatim}
;; app factory
(defn make-handler [app db]
  (fn [request]
    (app (assoc request :db db))))

;; Lifecycle
(start [this]
  (let [handler (make-handler app db)
        server (run-jetty handler options)]
    (assoc this :server server)))
\end{verbatim}

Пусть главная страница выводит данные из базы. Пример ниже показывает, как
выполнить запрос к базе из HTTP-запроса. Чтобы не усложнять пример версткой
HTML, вернем обычный текст.

\begin{verbatim}
(defn app [request]
  (let [{:keys [db]} request
        data (db/query db "select * from requests")]
    {:status 200
     :body (with-out-str
             (clojure.pprint/pprint data))}))
\end{verbatim}

Со временем приложению понадобятся другие компоненты, например очередь задач или
кэш. Их добавляют аналогично: добавляют зависимости серверу и пробрасывают в
\spverb|make-handler|.

Когда компонентов все больше, хранить их на верхнем уровне запроса неудобно:
возникает риск конфликта ключей. Поместим их во вложенный словарь
\spverb|:system| или \spverb|:engine|. Важно понимать, что \spverb|:system|
содержит не всю систему, а минимальное подмножество, необходимое для веб-части.

В случае с \emph{замыканием} компоненты передают в функцию отдельным
аргументом. С таким подходом обработчик принимает не один, а два аргумента:
подмножество системы и текущий запрос.

Чтобы выделить нужные компоненты, в систему вводят группировочный компонент. Он
ничего не делает при запуске и остановке, а только аккумулирует
зависимости. Сервер зависит от этого группировочного компонента. На базе него мы
строим дерево маршрутов (роутер), где каждый обработчик принимает компонент
первым аргументом.

Введем группировочный компонент \spverb|:web|. Пока что он зависит в только от
базы данных, но в будущем потребует и другие компоненты:

\begin{verbatim}
(defrecord Web [db])

(defn make-web []
  (-> (map->Web {})
      (component/using [:db])))
\end{verbatim}

В функции \spverb|make-system| подключим его в систему:

\begin{verbatim}
(component/system-map
 :web    (make-web)
 :server (make-server jetty)
 :db     (make-db pool)
 :worker (make-worker worker))
\end{verbatim}

Переключим зависимости сервера: теперь это не база данных, а группировочный
\spverb|web|:

\begin{verbatim}
(defrecord Server
  [options server web])

(defn make-server
  [options]
  (-> (map->Server {:options options})
      (component/using [:web])))
\end{verbatim}

Вспомним, как мы строили маршруты. Макрос \spverb|defroutes| возвращает функцию,
которая принимает запрос и возвращает ответ.

\begin{verbatim}
(defroutes app
  (GET "/"      request (page-index request))
  (GET "/hello" request (page-hello request))
  page-404)
\end{verbatim}

Теперь дерево маршрутов не статично, потому что зависит от компонента. Функция
\spverb|make-routes| принимает группировочный компонент и возвращает маршруты,
замкнутые на нем. В функции \spverb|page-index| и другие приходят два аргумента:
компонент и запрос. Компонент будет в том состоянии, что он пришел в
\spverb|make-routes|:

\begin{verbatim}
(defn make-routes [web]
  (routes
   (GET "/"      request (page-index web request))
   (GET "/hello" request (page-hello web request))))
\end{verbatim}

Метод \spverb|start| сервера строит маршруты и передает в \spverb|run-jetty|:

\begin{verbatim}
(start [this]
  (let [routes (make-routes web)
        server (run-jetty routes options)]
    (assoc this :server server)))
\end{verbatim}

Рассмотрим обработчик \spverb|page-index|, который обращается к базе
данных. Первый аргумент это хранилище компонентов, поэтому распакуем его на
уровне сигнатуры.

\begin{verbatim}
(defn page-index
  [{:keys [db]} request]
  (let [data (db/query db "select * from requests")]
    {:status 200
     :body (with-out-str
             (clojure.pprint/pprint data))}))
\end{verbatim}

Проброс и замыкание похожи: они решают одну и ту же задачу. Разница в том, как
технически передать аргументы в функцию. Проброс удобен тем, что обычно
HTTP-функции принимают один аргумент, и нам не придется менять сигнатуры.

С другой стороны, передача данных в запросе не всегда очевидна. Когда в запросе
много полей, становится трудно его постоить в тестах и разработке. При печати
запроса или записи в лог вы получите слишком большой выхлоп. Вариант с
замыканием и двумя аргументами выглядит понятнее. Сигнатура прямо говорит о том,
какие данные ожидают на входе. Выбор конкретного способа зависит от соглашений в
команде.

\subsection{Идемпотентность}

До сих пор мы писали компоненты так, что их повторный запуск приводил к
ошибке. Покажем это на примере веб-сервера:

\begin{verbatim}
(def s (-> {:port 8088 :join? false}
           make-server
           component/start))

(component/start s)
;; Execution error (BindException)
;; Address already in use
\end{verbatim}

В теле \spverb|start| мы не проверяем, что сервер уже работает. При попытке
включить уже запущенный сервер получим ошибку о том, что порт занят. Это
правильное поведение: мы бы не хотели, чтобы запустилось два сервера. Для других
компонентов исключения может не быть. Например, если повторно запустить базу
данных, получим новый пул соединений. Старый пул останется в памяти и будет
работать и писать логи. Так происходит утечка ресурсов.

Свойство, когда повторная операция возвращает тот же результат, называется
\emph{идемпотентность}. Чтобы избежать утечки ресурсов, компонент должен ему
следовать. Вызов \spverb|start| или \spverb|stop| работает один раз для данного
состояния.

На уровне кода мы проверяем слот перед тем, как открывать ресурс. Например, если
слот сервера \spverb|nil|, мы порождаем новый сервер и записываем в слот. Иначе
сервер уже запущен, поэтому возвращают \spverb|this|.

\begin{verbatim}
(start [this]
  (if server
    this
    (let [server (run-jetty app options)]
      (assoc this :server server))))
\end{verbatim}

Аналогично работает \spverb|stop|: перед тем, как закрыть ресурс, слот проверяют
на заполненность:

\begin{verbatim}
(stop [this]
  (when server
    (.stop server))
  (assoc this :server nil))
\end{verbatim}

Вариант с макросом \spverb|or| немного короче. Мы всегда записываем слот, но
значение это либо текущий сервер, либо новый.

\begin{verbatim}
(start [this]
  (let [server (or server (run-jetty app options))]
    (assoc this :server server)))
\end{verbatim}

\section{Integrant}

Библиотека Integrant\footurl{https://github.com/weavejester/integrant}
это следующий виток мысли о том, как строить системы. Мы поместили ее в конец
обзора по нескольким причинам. Integrant отталкивается от идей
Component, которые мы только что рассмотрели. Библиотека устроена гибче
и в целом более продвинута. Читатель должен подойти к ней с практическим опытом.

Как и в случае с Component, задача библиотеки~--- избежать глобального
состояния. Одновременно Integrant исправляет слабые моменты
Component. Перечислим тезисы, которые предложил автор библиотеки.

Сущности Component напоминают классы и ООП. В Clojure, где главную роль
играют данные и функции, это выглядит усложнением. Пусть компоненты будут
функциями. Функция проще объекта, потому что на нее действует только одна
операция~--- вызов.

Component выделяет только два состояния~--- \spverb|start| и
\spverb|stop|. Integrant предлагает дополнительные стадии, например
приостановку и возобновление, валидацию спекой, подготовку параметров. По
умолчанию эти стадии ничего не делают, но любой компонент может подписаться на
них. С таким подходом система гибче и удобней в поддержке.

Integrant делает ставку на \emph{декларативность}. Можно описать
систему в EDN-файле и считать одной функцией. Это выгодное отличие от
Component, где все делают вручную.

Integrant лоялен к зависимостям. Если в Component зависимость
требует два действия~--- добавить слот и метаданные,~--- то в Integrant
это один шаг. В Component зависимость может быть только другим
компонентом. Иногда объект оборачивают в компонент только чтобы выполнить это
требование. В Integrant зависимостью может быть что угодно: словарь,
символ, функция.

В целом Integrant выглядит легче и удобнее Component. Он
решает задачи простым способом, как это принято в Clojure.

\subsection{Базовое устройство}

Работу с Integrant начинают с описания будущей системы. Это словарь, за
который цепляется дальнейшая логика. Система ниже состоит из веб-сервера и пула
базы данных:

\begin{verbatim}
(def config
  {::server {:port 8080 :join? false}
   ::db {:username      "book"
         :password      "book"
         :database-name "book"
         :server-name   "127.0.0.1"
         :port-number   5432}})
\end{verbatim}

Ключ словаря это машинное имя компонента, а значение~--- параметры запуска. Уже
на этом этапе видно преимущество Integrant~--- декларативность. Эти
данные можно поместить в код или прочитать из файла.

Система и компоненты связаны через мультиметоды. Чтобы добавить реакцию на
событие, мы расширяем нужный мультиметод ключом компонента. Например, при старте
система вызывает метод \spverb|init-key| для каждого ключа. Чтобы объяснить
системе, как запускать сервер, метод расширяют ключом \spverb|::server|.

Integrant ожидает, что ключ реализует минимум два метода: запуск и
остановку. Это ключевые действия, поэтому для них нет реакции по
умолчанию. Другие события опциональны и остаются на ваше усмотрение.

\subsection{Первые компоненты}

Как и в прошлых разделах, мы напишем компоненты сервера и базы. Они просты и не
имеют зависимостей. Подготовьте модуль \spverb|integrant.clj| с шапкой:

\begin{verbatim}
(ns book.integrant
  (:require [integrant.core :as ig]))
\end{verbatim}

Для краткости мы опустим импорты \spverb|Jetty|, \spverb|HikariCP| и других
библиотек. Они аналогичны тем, что мы писали для Mount и
Component.

Начнем с сервера. Метод \spverb|init-key| принимает два параметра: ключ и
словарь опций. Это значения \spverb|::server| и \spverb|{:port 8080 :join? false}|
из конфигурации. Метод должен вернуть состояние компонента. В нашем
случае это результат \spverb|run-jetty|.

\begin{verbatim}
(defmethod ig/init-key ::server
  [_ options]
  (run-jetty app options))
\end{verbatim}

Ключ известен из определения метода, поэтому первый параметр затеняют
подчеркиванием. По аналогии опишем базу данных. Состояние компонента это
JDBC-спека с пулом соединений.

\begin{verbatim}
(defmethod ig/init-key ::db
  [_ options]
  {:datasource (cp/make-datasource options)})
\end{verbatim}

Функция \spverb|init| пробегает по системе и вызывает для каждого ключа
мультиметод \spverb|init-key|. Получим словарь, где ключ это имя компонента, а
значение~--- его \emph{состояние}:

\begin{verbatim}
(def _sys (ig/init config))

(keys _sys)
(:book.integrant/db :book.integrant/server)
\end{verbatim}

Остановка системы называется \spverb|halt|. Метод \spverb|halt-key!| определяет,
как выключить определенный ключ. Он принимает два параметра: ключ и состояние,
которые получили из метода \spverb|init-key|. Опишем эти события для сервера и
базы:

\begin{verbatim}
(defmethod ig/halt-key! ::server
  [_ server]
  (.stop server))

(defmethod ig/halt-key! ::db
  [_ db-spec]
  (-> db-spec :datasource cp/close-datasource))
\end{verbatim}

Функция \spverb|halt!| останавливает всю систему:

\begin{verbatim}
(ig/halt! _sys)
\end{verbatim}

\subsection{Зависимости}

Чтобы указать зависимости, в опции добавляют параметр-ссылку. При запуске
Integrant ищет ссылки в системе и строит по ним граф
зависимостей. Ссылку задают функцией \spverb|ig/ref|. Она принимает ключ, от
которого зависит компонент.

Рассмотрим зависимость на примере воркера. Добавьте в конфигурацию ключ как в
примере ниже. Чтобы отделить опции компонента от зависимостей, поместим их в
отдельное поле \spverb|:options|.

\begin{verbatim}
{::worker {:options {:sleep 1000}
           :db (ig/ref ::db)}}
\end{verbatim}

Когда \spverb|init-key| дойдет до ключа \spverb|::worker|, в поле \spverb|:db|
будет значение, которое \spverb|init-key| вернул для этого ключа. Приведем
реализацию \spverb|init-key| и \spverb|halt-key!|. Если вы забыли, как устроен
воркер, обратитесь к прошлым разделам главы.

\begin{verbatim}
(defmethod ig/init-key ::worker
  [_ {:keys [db options]}]
  (let [flag (atom true)
        task (make-task db flag options)]
    {:flag flag :task task}))

(defmethod ig/halt-key! ::worker
  [_ {:keys [flag task]}]
  (reset! flag false)
  (while (not (realized? task))
    (Thread/sleep 300)))
\end{verbatim}

\subsection{Параллели с Component}

Многие из приемов, что мы рассмотрели для Component, работают и в
Integrant Вспомним некоторые их них.

\textbf{Глобальное хранилище.} Чтобы управлять системой, нужно где-то ее
хранить. Проще всего добавить глобальную переменную и вспомогательные функции
для запуска и остановки.

\begin{verbatim}
(defonce ^:private system nil)

(def alter-system (partial alter-var-root #'system))

(defn system-start []
  (alter-system (constantly (ig/init config))))

(defn system-stop []
  (alter-system ig/halt!))
\end{verbatim}

Как и в Component, система должна быть приватной. Недопустимо, чтобы
компоненты свободно обращались к ней.

\textbf{Ожидание и сигналы.} Перед тем как закончить работу, приложение ожидает,
пока все компоненты остановятся. Макрос \spverb|with-handler| и перехват
сигналов работает аналогично для Integrant:

\begin{verbatim}
(with-handler :term
  (log/info "caught SIGTERM, quitting")
  (system-stop)
  (log/info "all components shut down")
  (exit))
\end{verbatim}

\textbf{Спуск системы и маршруты.} В Integrant легче обратиться к
системе из HTTP-запроса. Обработчик может быть компонентом и зависеть от базы.
Представим, что главная страница выводит число записей в базе. Добавим в систему
новый ключ и ссылку на базу:

\begin{verbatim}
{::handler {:db (ig/ref ::db)}}
\end{verbatim}

При запуске ключа вернем обработчик запроса, замкнутый на \spverb|db|:

\begin{verbatim}
(defmethod ig/init-key ::handler
  [_ {:keys [db]}]
  (fn [request]
    (let [query "select count(*) as total from requests"
          result (jdbc/query db query)
          total (-> result first :total)]
      {:status 200
       :body (format "You've got %s requests." total)})))
\end{verbatim}

Доработаем сервер, чтобы он зависел от обработчика:

\begin{verbatim}
{::server {:options {:port 8080 :join? false}
           :handler (ig/ref ::handler)}}

(defmethod ig/init-key ::server
  [_ {:keys [handler options]}]
  (run-jetty handler options))
\end{verbatim}

Теперь браузер покажет фразу <<You've got N requests>>, где \spverb|N|~--- число
записей в базе. Как и в случае с Component, \spverb|::handler| может
вернуть дерево маршрутов, построенное с помощью \spverb|Compojure|.

\textbf{Условное построение.} Перед запуском в систему можно изменить по
условию, как мы делали это с Component. Например, специальный флаг
определяет, будет ли запущен воркер или нет. Если да, поместим в систему ключ и
настройки.

\begin{verbatim}
(cond-> sys-config
  (is-worker-supported?)
  (assoc ::worker {:options {:sleep 1000}
                   :db (ig/ref ::db)}))
\end{verbatim}

Есть и другой способ запустить подмножество системы, похожий на
Mount. Функция \spverb|init| принимает необязательный список ключей,
которые следует включить. Список можно подготовить заранее по какому-то правилу.

\begin{verbatim}
(let [components (-> config keys set)
      components (cond-> components
                   (not (is-worker-supported?))
                   (disj ::worker))]
  (ig/init config components))
\end{verbatim}

\subsection{Проблема потери ключей}

Для компонентов указывают полные (квалифицированные) ключи, например
\spverb|::server| или \spverb|::db|. Двойное двоеточие означает текущее
пространство имен, в котором объявлен ключ. Запись \spverb|::db| это краткий
вариант \spverb|:book.integrant/db|.

Когда ключ полный (с пространством), легко определить, в каком модуле он
объявлен. В боевых системах бывает более десяти компонентов. Представьте,
возникла проблема с ключом \spverb|:queue|. Как понять, в каком месте мы
расширили мультиметод этим ключом? Наоборот, ключ
\spverb|:my-project.utils.queue/queue| несет эту информацию. Всегда используйте
полные ключи.

Возможна ситуация, когда вы забыли импортировать модуль, в котором расширили
мультиметод. Если модуль не загружен, Integrant не узнает о новом
компоненте. Иногда это вгоняет в ступор: вы точно помните, что писали код. Чтобы
избежать ошибки, добавьте все модули с мультиметодами в \spverb|ns| главного
модуля, который загружается всегда. Пусть это будет модуль системы.

\begin{verbatim}
(ns project.system
  (:require project.db
            project.server
            project.worker
            project.utils.queue))
\end{verbatim}

Утилиты для проверки синтаксиса (линтеры) могут выдать предупреждение. С их
точки зрения вы добавили модуль, но не используете его, потому что в коде нет
выражения \spverb|project.db/<something>|. Чтобы подавить предупреждения,
исправьте конфигурацию линтера. Добавьте модули в секцию <<known namespaces>>
или аналогичную.

Integrant предлагает функцию \spverb|load-namespaces| для
автоматической загрузки модулей. На вход подают конфигурацию системы. Для
каждого ключа функция вычисляет его пространство и загружает его. Вот как
выглядит промышленная система с ключами из разных модулей:

\begin{verbatim}
(def config
  {:project.server/server
   {:options {:port 8080 :join? false}
    :handler (ig/ref :project.handlers/index)}
   :project.db/db {...}
   :project.worker/worker
   {:options {:sleep 1000}
    :db      (ig/ref :project.db/db)}
   :project.handlers/index
   {:db (ig/ref :project.db/db)}})
\end{verbatim}

Чтобы загрузить все модули, которые участвуют в системе, выполните:

\begin{verbatim}
(ig/load-namespaces config)
\end{verbatim}

Советуем новичкам воздержаться от автоматических импортов. Размещайте их явно в
блоке \spverb|ns|: этот вариант хоть и многословен, но очевиден. Прибегайте к
\spverb|load-namespaces| только если точно знаете, как работают пространства
имен в Clojure.

\subsection{Система в файле}

Мы упоминали, что Integrant делает ставку на
декларативность. Конфигурация системы это статичная структура данных,
словарь. Для экономии места систему выносят в EDN-файл и читают функцией из
модуля \spverb|clojure.edn|.

Читатель заметит, что мы указали ссылки через функцию \spverb|ig/ref|, и не
совсем ясно, как записать это выражение в файл. Помогут теги: при чтении EDN мы
указываем связь между тегом и функцией, которая обработает следующее за тегом
значение. Тег \spverb|#ig/ref| действует как одноименная функция.

\begin{verbatim}
{:project.worker/worker {:options {:sleep 1000}
                         :db #ig/ref :project.db/db}}
\end{verbatim}

Integrant предлагает функцию \spverb|read-string| что чтения EDN. Это обертка
вокруг обычного \spverb|clojure.edn/read-string|, но с дополнительными
тегами. Чтобы прочитать систему из файла, выполните:

\begin{verbatim}
(def config
  (-> "config.edn" slurp ig/read-string))
\end{verbatim}

Из главы про конфигурацию мы помним, что нежелательно хранить в файле пароли и
ключи доступа \page{password-note}. Компонент \spverb|:project.db/db| нарушает
этот принцип: пароль к базе записан открыто. Сделаем так, чтобы парсер читал
пароль из переменной среды.

Поместим конфигурацию в файл \spverb|integrant.test.edn| (ниже ее фрагмент):

\begin{verbatim}
{:project.db/db {:password #env DB_PASSWORD}
 :project.worker/worker {:options {:sleep 1000}
                         :db #ig/ref :project.db/db}}
\end{verbatim}

Обернем чтение конфигурации в функцию. Первым аргументом в
\spverb|ig/read-string| укажем словарь с тегами. Функцию \spverb|tag-env| для
тега \spverb|#env| возьмем из прошлой главы. На нижнем уровне Integrant
дополнит словарь тегов собственными, поэтому оба \spverb|#ig/ref| и
\spverb|#env| будут работать.

Теперь система хранится в файле, а теги описывают ее точнее и гибче.

\begin{verbatim}
(defn load-config [filename]
  (ig/read-string {:readers {'env tag-env}}
                  (slurp filename)))

(load-config "integrant.test.edn")
{:project.db/db {:password "c8497b517da25"}
 :project.worker/worker
 {:options {:sleep 1000}
  :db #integrant.core.Ref{:key :project.db/db}}}
\end{verbatim}

\subsection{Наследование ключей}

В Clojure ключи могут строиться в иерархию. Функция \spverb|derive| принимает
два ключа и задает \emph{превосходство} первого над вторым.

\begin{verbatim}
(derive ::postgresql ::database)
\end{verbatim}

Когда мультиметод ищет действие по ключу, он учитывает иерархию. Например, если
мультиметод задан для \spverb|::database|, вызов с \spverb|::postgresql| не
приведет к ошибке: сработает версия \spverb|::database|.

Поскольку Integrant устроен на мультиметодах, из наследования можно
извлечь пользу. Представим нагруженный проекты с двумя базами данных: мастер для
записи и реплика для чтения. Пусть это компоненты \spverb|::db-master| и
\spverb|::db-replica|. Технически они одинаковы и отличаются только входными
параметрами.

Если бы мы не знали про наследование, то расширили бы \spverb|ig/init-key| и
\spverb|ig/halt-key!| каждым ключом. Это дублирует код и считается плохой
практикой. Мы уже описали компонент \spverb|::db|. Унаследуем от него две других
базы:

\begin{verbatim}
(derive ::db-master ::db)
(derive ::db-replica ::db)
\end{verbatim}

Напишем конфигурацию для двух баз. Для реплики выставим флаг \spverb|:read-only|
\spverb|true|, чтобы обезопасить себя от записи не в тот источник. Обратите
внимание на зависимости. Поскольку воркер пишет данные в базу, он ссылается на
\spverb|::db-master|. Компонент \spverb|::hander| только читает данные, поэтому
зависит от \spverb|::db-replica|.

\begin{verbatim}
(def config
  {::server {:options {:port 8080 :join? false}
             :handler (ig/ref ::handler)}
   ::db-master {;; other fields
                :read-only false}
   ::db-replica {;; other fields
                 :read-only true}
   ::worker {:options {:sleep 1000}
             :db (ig/ref ::db-master)}
   ::handler {:db (ig/ref ::db-replica)}})
\end{verbatim}

Функция и тег \spverb|ig/refset| вернут множество ссылок с учетом
иерархии. Предположим, один из компонентов ожидает \emph{все} базы данных для
ручной синхронизации. Чтобы не ссылаться на каждую базу вручную, укажем корневой
ключ \spverb|::db|.

Добавим компонент \spverb|::sync| для синхронизации баз. Укажем зависимость
\emph{от всех} баз через \spverb|refset|. Метод \spverb|init-key| вернет фоновую
задачу, которая раз в интервал сверяет данные. Запуск и остановка задачи
аналогичны воркеру. Пример ниже показывает, как добраться до обеих баз из
параметров.

\begin{verbatim}
{::sync {:dbs (ig/refset ::db)}}

(defmethod ig/init-key ::sync
  [_ opt]
  (let [{:keys [dbs]} opt
        [db-master db-replica] dbs]
    (run-task db-master db-replica)))
\end{verbatim}

\subsection{Другие стадии компонента}

Кроме запуска и остановки, Integrant выделяет другие стадии, которые
проходит компонент. Они не обязательны к реализации. Стадии устроены как
мультиметоды, которым задано действие по умолчанию (вернуть \spverb|nil| или
исходный объект). Чтобы подписать компонент на событие, расширите мультиметод
его ключом. Ниже мы рассмотрим несколько полезных стадий.

\subsection{Подготовка}

Метод \spverb|ig/prep-key| служит для предварительной подготовки
параметров. Чаще всего это объединение параметров по умолчанию с теми, что
пришли из конфигурации. Например, методом проб мы нашли идеальные метрики пула
БД. Чтобы не указывать в конфигурации все поля, фиксированную часть в словарь по
умолчанию.

\begin{verbatim}
(def db-defaults
  {:auto-commit        false
   :read-only          false
   :connection-timeout 30000
   :validation-timeout 5000
   :idle-timeout       600000
   :max-lifetime       1800000
   :minimum-idle       10
   :maximum-pool-size  10})

(defmethod ig/prep-key ::db
  [_ options]
  (merge db-defaults options))
\end{verbatim}

Метод \spverb|prep-key| объединяет этот словарь с параметрами. В конфигурации
достаточно указать только параметры подключения и, если требуется,
переопределения:

\begin{verbatim}
{::db {:auto-commit   true ;; override the default
       :adapter       "postgresql"
       :username      "book"
       :password      "book"
       :database-name "book"
       :server-name   "127.0.0.1"
       :port-number   5432}}
\end{verbatim}

Функция \spverb|ig/prep| принимает конфигурацию и запускает метод для каждого
ключа. Чтобы не забыть этот шаг, добавьте его в \spverb|load-config|.

\subsection{Спека}

Метод \spverb|ig/pre-init-spec| связывает параметры компонента со спекой. Если
метод вернул спеку, параметры проходят проверку. Например, для базы данных
обязательны параметры подключения. Проверим их перед запуском пула:

\begin{verbatim}
(require '[clojure.spec.alpha :as s])

(s/def :db/username string?)
(s/def :db/database-name string?)
;; etc

(defmethod ig/pre-init-spec ::db [_]
  (s/keys :req-un [:db/username
                   :db/password
                   :db/database-name
                   :db/server-name
                   :db/port-number]))
\end{verbatim}

Если запустить систему с неверными параметрами, получим ошибку \spverb|spec|.

\subsection{Приостановка}

Кроме \spverb|init| и \spverb|halt|, Integrant выделяет третье
состояние системы~--- \spverb|suspended|. Приостановленный компонент не теряет
состояние, а только ставит на паузу внутренние процессы. Например, если это
потребитель сообщений из очереди \spverb|(KafkaConsumer)|, он не закрывает
соединение, а временно перестает читать сообщения (вызывать метод
\spverb|poll|). Обратная операция называется \spverb|resume|. При возобновлении
компонент, не порождая новых соединений, продолжает работу.

По умолчанию эти события работают как \spverb|halt| и \spverb|init|. Чтобы
задать особую реакцию на \spverb|suspend| и \spverb|resume|, задайте методы
\spverb|ig/suspend-key!| и \spverb|ig/resume-key|. Это потребует углубленного
чтения документации. Оставим тему читателю на самостоятельное изучение.

\section{Заключение}

Подобно тому, как машина складывается из деталей, программа состоит из
компонентов. Ими управляет система~--- соглашение о том, как устроены и связаны
компоненты друг с другом.

Любой проект нуждается в системе, и чем дольше он развивается, тем сильнее
потребность. Если в проекте нет соглашения о том, как писать составные части, он
начинает буксовать. Поддержка проекта станет слишком затратной.

Clojure предлагает разные подходы для систем. Проекты Mount, Component и
Integrant наиболее популярны. Они исповедуют разный подход, так что разработчик
найдет то, что ему по душе.

Mount отталкивается от глобальных переменных. Если проект написан в таком стиле:

\begin{verbatim}
(def server (run-jetty app {:port 8080}))
\end{verbatim}

\noindent
, то портировать его на Mount будет легко. Переменная \spverb|server| станет
сущностью, которая меняет значение по команде. Mount подойдет тем, кто только
начал знакомство с Clojure.

Component это шаг в сторону настоящих компонентов. Это сущности, которые
изолируют состояние. Компоненты и протоколы напоминают классы и объекты и из
современных языков программирования. По этой причине некоторые недолюбливают
Component и обвиняют в излишней раздутости, <<энтерпрайзности>>.

Действительно, иногда решение на компонентах занимает больше места, чем на
атомах и функциях. С другой стороны, именно Component дает понимание того, как
строить устойчивые системы. Заметим, что б\'{о}льшую часть вопросов мы обсудили
в разделе именно про Component.

Проект Integrant ставит цель исправить недостатки Component. Он лишен тяжести
ООП и целом более <<кложурный>>. Integrant опирается на идиомы и техники,
принятые в Clojure, и тем самым подкупает опытных разработчиков.

Мы не ставим цель выяснить, какая из библиотек лучше. Не бросайтесь переписывать
проект с условного Mount на Component или наоборот. Это изнуряющий труд, и вы не
поймете, каких преимуществ достигли, пока не ощутите в них потребность.

Вместо споров о том, \emph{какая} система лучше, подумайте \emph{зачем} система
нужна проекту. Когда системный подход очевиден, технические решения найдутся
сами.
